/////////////////
// Level t1l2 //
///////////////


///////////////////////
// LEVEL INITIALIZE //
/////////////////////

level.script="maps/t1l2.scr"
level.music="t1l2"


exec global/auto.scr
fadeout .001 0 0 0 1

level waittill prespawn

$world northyaw $northyaw.angle

$world farplane_color (0.038 .058 .099)
$world farplane 2900 //this must stay at 3000 for framerate reasons

level waittill spawn

setcvar g_mission 1

level.friendly1.maxhealth = 500
level.friendly1.health = 500
level.friendly2.maxhealth = 500
level.friendly2.health = 500
level.friendly3.maxhealth = 500
level.friendly3.health = 500

level.friendly1 gun sten
level.friendly2 gun enfield
level.friendly3 gun enfield

$Flak88A_turret0 turnspeed 15
$Flak88A_turret0 pitchspeed 7.5
$Flak88A_turret0 viewjitter 7
$Flak88A_turret0 firedelay 4
$Flak88A_turret0 maxyawoffset 20.0
$Flak88A_turret0 pitchcaps "20 10 0"
$Flak88A.collisionent = $flak88_collision
$Flak88A_turret0.collisionent = $flak88_turret_collision
$Flak88A lock

$Flak88B_turret0 turnspeed 15
$Flak88B_turret0 pitchspeed 7.5
$Flak88B_turret0 viewjitter 7
$Flak88B_turret0 firedelay 4
$Flak88B_turret0 pitchcaps "20 10 0"
$Flak88B.collisionent = $flak88_collision
$Flak88B_turret0.collisionent = $flak88_turret_collision
$Flak88B lock

// update damage for the flak88 throbbing boxes...
$Flak1ThrobbingBox SetDamage 2000
$Flak1ThrobbingBox Radius 300
$Flak1ThrobbingBox explosionoffset "0 0 128"

$Flak88_2 SetDamage 2000
$Flak88_2 Radius 800
$Flak88_2 explosionoffset "0 0 300"

exec gags/T1L2_Flak88.Scr
exec gags/T1L2_Friend.Scr

wait 0.25	// need to pause for map startup or something?

level.bTrainReady = 0


level.cappy	= level.friendly1

level.friendly2 thread gags/t1l2_friend.scr::Init
level.friendly3 thread gags/t1l2_friend.scr::Init
level.cappy thread gags/t1l2_cappy.scr::main

level.friendly2.targetname = Friendly2
level.friendly3.targetname = Friendly3


$player stufftext "tmstart sound/music/Emmerich.mp3"

// make sure our friendlies don't die....
/*
level.cappy nodamage
level.friendly2 nodamage
level.friendly3 nodamage	// he'll die later!
*/

level.nFlakGermans = 0
level.nFlakGermans2 = 0

// Setup some enemies / objects....

// Barn Flak guys...
$FlakGerman1 thread WaitForFlakDeath
$FlakGerman2 thread WaitForFlakDeath
$FlakGerman3 thread WaitForFlakDeath

$FlakGerman4 thread WaitForFlakDeath2
$FlakGerman5 thread WaitForFlakDeath2
$FlakGerman6 thread WaitForFlakDeath2

$player item items/binoculars.tik

$MG42Guy type_idle machinegunner
$MG42Guy type_attack machinegunner
$MG42Guy type_disguise machinegunner
$MG42Guy type_grenade machinegunner
$MG42Guy turret $mg42
$MG42Guy exec global/setdeathanim.scr "death_fall_back"

$Flak1ThrobbingBox hide	// captain will show this..

$Objective1 text "Locate and Destroy Artillery Emplacements[2 remaining]"
$Objective1 SetCurrent
$Objective1 TurnOn

level.nRequiredObjectives = 2
level.nWaitingForPlayer = 0

////Truck Stuff
thread SetupTruck
$truck_1 vehicleanim idlenolights

$barndoor_lockedtrigger thread global/door_locked.scr::door_locked_barn
$biggate_lockedtrigger thread global/door_locked.scr::door_locked_barn
$lastdoor_lockedtrigger thread global/door_locked.scr::door_locked_wood1
$lastgate_lockedtrigger thread global/door_locked.scr::door_locked_barn

$player fullheal

	freezeplayer
	$player.viewangles = "0 160 0"
	//dprintln "camera should be frozen"
	wait 3
	releaseplayer
	$player.viewangles = "0 160 0"
	//dprintln "camera unlocked"


end


//-------------------------------------------------------------------
SetupTruck:
//
//	Truck on path
//-------------------------------------------------------------------

	level.bDoTruck		= 0

	$truck_1 waitthread gags/t3l1_enemyspawn.scr::DoTruck 1800 2 0

	// give them guns...
	$truck_1.passenger[1] gun "mp40"
	$truck_1.passenger[2] gun "Mauser KAR 98K"
//	$truck_1.passenger[3] gun "Mauser KAR 98K"
//	$truck_1.passenger[4] gun "mp40"

// make sure those pesky friendlyies don't go after these guys...
	$truck_1.passenger[1] threatbias ignoreme
	$truck_1.passenger[2] threatbias ignoreme
	$truck_1.driver threatbias ignoreme

	$truck_1.passenger[1] targetname barn_outside_guys
	$truck_1.passenger[2] targetname barn_outside_guys
	$truck_1.driver targetname barn_outside_guys


	$trigger_truck waittill trigger
//	$truck_1 playsound opeltruck_snd_start
	wait .5
	$truck_1 vehicleanim idlelights
	// make truck go and wait till it's done moving...
	$truck_1 waitthread gags/t3l1_enemyspawn.scr::TruckDrive $truck_1path 0 400 0 0 0
	// turn off our lights when we get here....
	wait .5
//	$truck_1 playsound opeltruck_snd_stop
	$truck_1 vehicleanim idlenolights

	$truck_1.passenger[1] threatbias 1000
	$truck_1.passenger[2] threatbias 1000
	$truck_1.driver threatbias 1000

End




///////////////////
///Patrol Groups///
///////////////////

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Group 1
//
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Patrol1Go:

	wait 2
	$Patrol1 thread Patrol1walking
	$Patrol1b thread Patrol1bwalking
	$Patrol2 thread Patrol2walking

end

Patrol1walking:

	self walkto $Patrol1Path2
	self waittill movedone
	if ( parm.movedone == 1 )
	{
		wait 3
	}
	self walkto $Patrol1Path
	self waittill movedone
	if ( parm.movedone == 1 )
	{
		self delete
	}

end

Patrol1bwalking:

	self walkto $Patrol1Path2b
	self waittill movedone
	if ( parm.movedone == 1 )
	{
		wait 3
	}
	self walkto $Patrol1Path
	self waittill movedone
	if ( parm.movedone == 1 )
	{
		self delete
	}

end


//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Group 2
//
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~



Patrol2walking:

	wait 25
	self walkto $Patrol2Path
	self waittill movedone
	if ( parm.movedone == 1 )
	{
		self delete
	}

end


//_____________________
// FIRST HOUSE TRIGGERS
//_____________________

spawnset01:
//	iprintln "T01: WINDMILL SNIPER A CREATED, SNIPER B DELETED"
	thread global/ai.scr::spawn 01
	$triggerset02 delete
end


spawnset02:
//	iprintln "T02: WINDMILL SNIPER B CREATED, SNIPER A DELETED"
	thread global/ai.scr::spawn 02
	$triggerset01 delete
end


spawnset03:
//	iprintln "T03: FIRST HOUSE GERMANS CREATED, CROSSFIRE DELETED"
	thread global/ai.scr::spawn 03
	$triggerset05 delete
end


spawnset04:
//	iprintln "T04: FIRST GARAGE GERMANS CREATED, CROSSFIRE DELETED"
	thread global/ai.scr::spawn 04
	$triggerset06 delete
end


spawnset05:
//	iprintln "T05: FIRST HOUSE CROSSFIRE GERMANS CREATED, FIRST HOUSE DELETED"
	thread global/ai.scr::spawn 05
	$triggerset03 delete
end


spawnset06:
//	iprintln "T06: FIRST GARAGE CROSSFIRE GERMANS CREATED, FIRST GARAGE DELETED"
	thread global/ai.scr::spawn 06
	$triggerset04 delete
end


spawnset07:
//	iprintln "T07: FIRST HOUSE GERMAN PATROLLERS CREATED"
	thread global/ai.scr::spawn 07
end

//_____________________________
// FIRST ARTILLERY GUN TRIGGERS
//_____________________________

spawnset08:
//	iprintln "T08: ARTILLERY BARN SQUAD 1-3 CREATED, AMBUSH DELETED"
	thread global/ai.scr::spawnset 08 "barn_outside_guys"
	$triggerset10 delete
end


spawnset09:
//	iprintln "T09: ARTILLERY BARN SQUAD 4-6 HOUSE REAR CREATED, FRONT DELETED"
	thread global/ai.scr::spawnset 09 "barn_outside_guys"
	$triggerset11 delete
end


spawnset10:
//	iprintln "T10: ARTILLERY BARN SQUAD AMBUSH CREATED, 1-3 & 4-6 DELETED"
	thread global/ai.scr::spawnset 10 "barn_outside_guys"
	$triggerset08 delete
	$triggerset09 delete
end


spawnset11:
//	iprintln "T11: ARTILLERY BARN SQUAD 4-6 HOUSE FRONT CREATED, REAR DELETED"
	thread global/ai.scr::spawnset 11 "barn_outside_guys"
	$triggerset09 delete
end

//_____________________
// AMBUSH BARN TRIGGERS
//_____________________

spawnset12:
//	iprintln "T12: BARN CARDTABLE GERMANS CREATED"
	thread global/ai.scr::spawn 12
end


spawnset13:
//	iprintln "T13: BARN INTERIOR - CARDTABLE ENTRANCE GERMANS CREATED, MAIN DELETED"
	thread global/ai.scr::spawn 13
	$triggerset14 delete
end


spawnset14:
//	iprintln "T14: BARN INTERIOR - MAIN ENTRANCE GERMANS CREATED, CARDTABLE DELETED"
	thread global/ai.scr::spawn 14
	$triggerset13 delete
end


spawnset15:
//	iprintln "T15: BARN UPPER LEVEL - BARN TRIGGER GERMANS CREATED, OUTSIDE DELETED"
	thread global/ai.scr::spawn 15
	$triggerset16 delete
end

spawnset16:
//	iprintln "T16: BARN UPPER LEVEL - OUTSIDE TRIGGER GERMANS CREATED, BARN DELETED"
	thread global/ai.scr::spawn 16
	$triggerset15 delete
end


spawnset17:
//	iprintln "T17: BARN YARD GERMANS - INSIDE CREATED, HOUSE SIDE DELETED "
	thread global/ai.scr::spawn 17
	$triggerset18 delete
end


spawnset18:
//	iprintln "T18: BARN YARD GERMANS - OUTSIDE CREATED, BARN SIDE DELETED"
	thread global/ai.scr::spawn 18
	$triggerset17 delete
end


//________________________
// FINAL COMPOUND TRIGGERS
//________________________

spawnset19:
	exec global/autosave.scr 3
//	iprintln "T19: COMPOUND WINDOW GERMANS CREATED"
	wait 1.5
	thread global/ai.scr::spawn 19
end


spawnset20:
//	iprintln "T20: COMPOUND BARN GERMANS CREATED"
	thread global/ai.scr::spawn 20
end


spawnset21:
//	iprintln "T21: COMPOUND HOUSE 1 GROUND GERMANS CREATED"
	thread global/ai.scr::spawn 21
end


spawnset22:
//	iprintln "T22: COMPOUND HOUSE 1 UPPER GERMANS CREATED"
	thread global/ai.scr::spawn 22
end


spawnset23:
//	iprintln "T23: COMPOUND HOUSE 2 GROUND GERMANS CREATED"
	thread global/ai.scr::spawn 23
end


spawnset24:
//	iprintln "T24: COMPOUND LIBRARY A GERMANS CREATED, B DELETED"
	thread global/ai.scr::spawn 24
	$triggerset25 delete
end

spawnset25:
//	iprintln "T25: COMPOUND LIBRARY B GERMANS CREATED, A DELETED"
	thread global/ai.scr::spawn 25
	$triggerset24 delete
end


spawnset26:
//	iprintln "T26: COMPOUND HOUSE 2 STAIRS GERMAN CREATED"
	thread global/ai.scr::spawnset 26 upstairs_guys
end


spawnset27:
//	iprintln "T27: COMPOUND HOUSE 2 UPPER GERMANS CREATED"
	thread global/ai.scr::spawnset 27 upstairs_guys
end


spawnset28:
//	iprintln "T28: COMPOUND HOUSE 2 CATWALK GERMANS CREATED"
	thread global/ai.scr::spawnset 28 upstairs_guys
end


spawnset29:
//	iprintln "T29: COMPOUND TENT GERMANS CREATED"
	thread global/ai.scr::spawn 29
end


spawnset00:
//	iprintln "spawnset00 trigger fired"
	thread global/ai.scr::spawn 00
	// $triggername delete
end


/////////////////////
// Jeff's Threads //
///////////////////

//------------------------------------------------------------------
SpawnOutsideBarn:
//
//	tell cappy to do the spawn, then remove this trigger...
//------------------------------------------------------------------

//	iprintln "SECOND HOUSE GERMANS CREATED"	

	level.cappy thread gags/t1l2_cappy.scr::SpawnOutsideBarn

	self delete
	
	End

//------------------------------------------------------------------
WaitForFlakDeath:
//
// tells cappy to blow the flak88 when all these guys are dead...
//------------------------------------------------------------------

	level.nFlakGermans++

	self waittill death

	level.nFlakGermans--

	if ( level.nFlakGermans==0 )
	{
		level.cappy thread gags/t1l2_cappy.scr::BlowFlak88
	}

	End

//------------------------------------------------------------------
WaitForFlakDeath2:
//
// tells cappy to stop covering fire when they're all dead...
//------------------------------------------------------------------


	level.nFlakGermans2++

	self waittill death

	level.nFlakGermans2--

	if ( level.nFlakGermans2==0 )
	{
		level.cappy thread gags/t1l2_cappy.scr::CoverFireStop
	}

	End

//------------------------------------------------------------------
Flak88GonnaGoBoom:
//------------------------------------------------------------------

	$player stufftext "tmstart sound/music/Gotha.mp3"

	level.cappy thread gags/t1l2_cappy.scr::SpawnInvestigators
	

	$player stufftext "tmstart sound/music/Gotha.mp3"
	thread DeadFlakCollision
	wait 10
	thread global/exploder.scr::explode 7
	earthquake 1 1 0 0
	End

DeadFlakCollision:

	wait 9.9
	$Flak88A thread global/stationaryweapons.scr::SpawnCollision $flak88_destroyed_collision
end


//------------------------------------------------------------------
Objective1Complete:
//
//	Trigger sent via ThrobbingBox
//------------------------------------------------------------------

//	$Objective1 text "Locate and Destroy Artillery Emplacements[1 remaining]"
	$Objective1.target = $Objective2.target
//	$Objective1 Turnoff
	$Objective2 text "Locate and Destroy Artillery Emplacements[1 remaining]"
	$Objective2 TurnOn
	$Objective2 SetCurrent

	level.nRequiredObjectives--
		
	End

//------------------------------------------------------------------
Objective2Complete:
//
//	Trigger sent via ThrobbingBox
//------------------------------------------------------------------

	$Flak88B thread global/stationaryweapons.scr::SpawnCollision $flak88_destroyed_collision
	$Objective2 text "Locate and Destroy Artillery Emplacements[0 remaining]"
	$Objective2 Complete
	$Objective3 SetCurrent

	level.nRequiredObjectives--

	// tell cappy about it...
	level.cappy thread gags/t1l2_cappy.scr::Flak88_2_BlownUp


	End

//------------------------------------------------------------------
Objective3Complete:
//
//	Trigger sent via trigger_multiple
//------------------------------------------------------------------

	if ( level.nRequiredObjectives!=0 )
	{
//		dprintln "Not all objectives are complete!"
		End
	}

	self delete

	$Objective3 Complete
	exec global/missioncomplete.scr t1l3 1

	end

//------------------------------------------------------------------
runandjumpguy:
//
//	This trigger tells friendly2 to go and jump over the wall.
//------------------------------------------------------------------

	if ( level.bJumpOverWall==1 )
	{
		level.longtermfriend thread gags/t1l2_friend.scr::JumpOverWall
		self delete
	}
	else
	{
//		iprintln "Not ready to jump over wall!"
	}


	End

//------------------------------------------------------------------
UnderBridgeTrigger:
//
//	Just tell the captain to run to his grenade spot...
//
//------------------------------------------------------------------

//	make sure we kill the grunt if he's the one that stepped on the
//	trigger...


//	level.cappy runto $ThrowGrenade2

//	iprintln "HIT UNDER BRIDGE TRIGGER!"

///////	level.friendly3 origin $FriendlyCheat.origin
	if ( level.friendly3dead==0 )
		level.friendly3 thread gags/t1l2_friend.scr::GetKilled
	else
		level.friendly2 thread gags/t1l2_friend.scr::GetKilled

	// take this out when the captain can crawl...
//	$MG42Guy exec global/disable_ai.scr

	self delete

	End

//------------------------------------------------------------------
MG42Trigger:
//
//	Somebody stepped on the MG42 trigger.
//	No matter who it is, start the gag...
//
//------------------------------------------------------------------

	if (parm.other!=level.friendly3 && parm.other!=level.friendly2)
	{
		End
	}

	self delete

	$machinegun accuracy 0
	
		// if only 1 guy left, make the shot miss...
	if ( level.friendly3dead==1 || level.friendly2dead==1 )
		wait 0.2

	$machinegun turnon
	level.cappy thread gags/t1l2_cappy.scr::DoFlushOut

	$player stufftext "tmstart sound/music/Labyrinth.mp3"

	wait 0.4
	if ( level.friendly3dead==0 && level.friendly2dead==0 )
	{
		level.friendly3 damage level.friendly3 3000 level.friendly3 (0 0 0) (0 0 0) (0 0 0) 0 9 0 0
	}

	wait 0.4

	$machinegun turnoff

//	wait 0.5

	// after first shot, make him not so good...
//	$machinegun convergeTime 2



	End

//------------------------------------------------------------------
CappyPlayFlushout:
//
//	Trigger setup in the spot where the captain should say his thing.
//
//------------------------------------------------------------------

/*
	if ( parm.other==level.cappy )
	{
		level.cappy thread gags/t1l2_cappy.scr::PlayFlushOut
		self delete
	}
	*/

	End

//------------------------------------------------------------------
DoTrain:
//
//		Send the train by and have cappy say his dialog...
//
//------------------------------------------------------------------

	// testtest
	if ( level.bTrainReady==1 )
	{
		// $Train Go
		thread global/spotlight.scr::traincorona $train1 "light middle"

		level.cappy thread gags/t1l2_cappy.scr::TrainDialog
		self delete
	}

	End

//------------------------------------------------------------------
DestroyFlak2:
//
//		Have the captain say his thing and head into clearing the
//		final house and checking out the last flak88
//
//------------------------------------------------------------------

	// start up the 2nd FLAK88....
	thread gags/T1L2_Flak88.Scr::DoFlak88Guys

	level.cappy thread gags/t1l2_cappy.scr::DestroyFlak2

	self delete


	End

//------------------------------------------------------------------
PlayerUpstairs:
//------------------------------------------------------------------
	// get rid of these guys because they break the gag...
	if ( $outsideguy_1!=NIL )
	{
		if ( isAlive $outsideguy_1 )
			$outsideguy_1 delete
	}

	if ( $outsideguy_2!=NIL )
	{
		if ( isAlive $outsideguy_2 )
			$outsideguy_2 delete
	}

	self delete

	/*
	done differently now...
	if (level.nWaitingForPlayer==1)
	{
		level.cappy thread gags/t1l2_cappy.scr::PlayerUpstairs
		self delete
	}
	*/

	End

//------------------------------------------------------------------
PlayerAtWindow:
//------------------------------------------------------------------

	if (level.nWaitingForPlayer==2)
	{
		level.cappy thread gags/t1l2_cappy.scr::PlayerAtWindow
		self delete
	}

	End

//------------------------------------------------------------------
PlayerOutside:
//------------------------------------------------------------------
	if (level.nWaitingForPlayer==3)
	{
		level.cappy thread gags/t1l2_cappy.scr::PlayerOutside
		self delete
	}
	
	End

//------------------------------------------------------------------
Flak88_2_GonnaGoBoom:
//------------------------------------------------------------------

	level.cappy thread gags/t1l2_cappy.scr::Flak88_2_GonnaGoBoom

	End


//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// stay on path enemies
//
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
stayonpath local.dumbdelay:

	local.bias = self.threatbias

//	println "~~~~~~~~~~~~~~~~~~~~~~~~~~~~ stayonpath " self.targetname ", " self.entnum

	self exec global/disable_ai.scr 
	self notsolid
	self hide
	self threatbias ignoreme

	self waittill trigger
//	println "~~~~~~~~~~~~~~~~~~~~~~~~~~~~ stayonpath " self.targetname ", " self.entnum " was triggered"

	self solid
	self show
	self threatbias local.bias

	self runto self.target
	wait local.dumbdelay

	self exec global/enable_ai.scr 

end


//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
triggerstayonpath local.trigger local.dumbdelay local.triggerthread:

	if ( local.trigger != NIL )
	{
		if ( local.trigger.target != NIL )
		{
			local.count = $(local.trigger.target).size

			if ( local.count > 0 )
			{
//				println "~~~~~~~~~~~~~~~~~~~~~~~~~~~~ triggerstayonpath initializing " local.count " guys linked to trigger " local.trigger.targetname
				local.targs = exec global/makearray.scr $(local.trigger.target)
				for ( local.i = 1; local.i <= local.count; local.i++ )
				{
					local.targs[local.i] thread stayonpath local.dumbdelay
				}
			}

			if ( local.triggerthread != NIL )
			{
				local.trigger.target = NULL
				local.trigger thread local.triggerthread
			}
//			else
//			{
//				local.trigger waittill trigger
//				waitframe
//				local.trigger remove
//			}
		}
	}

end


// ---------------------------------------------------------------
windmill_dialog:
// ---------------------------------------------------------------

// delete the trigger
	self delete

	if ( $windmill_sniper!=NIL && isAlive $windmill_sniper )
	{
		dprintln "DOING SNIPER DIALOG"
		//	"Sniper!" maps "t1l2  "
		if ( level.friendly2dead==0 )
			level.friendly1 say dfr_T1L2_BP6734
		else
			level.friendly2 say dfr_T1L2_BP6734
		
		wait 1

		level.cappy say dfr_T1L2_BC6961
	}
	else
	{
		dprintln "SNIPER not around for his dialog!"
	}

	end



// ---------------------------------------------------------------
artillery_dialog:
// ---------------------------------------------------------------

	self delete
	
	level.cappy say dfr_T1L2_BC6960

	end

// ---------------------------------------------------------------
stick_together_dialog:
// ---------------------------------------------------------------

	self delete
	level.cappy say dfr_T1L2_BC6964

	end

