
//-----------------------------------------------
//Init the Antenna
//
//
//	Values for moving the antenna:
//	
//	stage1
//	stage2 144
//	stage3 250
//-----------------------------------------------
init_antenna:
	//Bindage for Antenna Parts
	
	$antenna_dish bind $antenna_origin
	$antenna_origin bind $stage3
	$stage3 bind $stage2
	$stage2 bind $stage1
	for (local.i = 1; local.i <= $antenna_base.size; local.i++)
	{
		$stage1 bind $antenna_base[local.i]
		$antenna_base[local.i] bind $antenna_plat_origin
	}

	//Bindage for Antenna Doors
	
	$antenna_left_door bind $antenna_left_door_origin
	$antenna_right_door bind $antenna_right_door_origin
	

	$stage1.startorigin = $stage1.origin
	$stage2.startorigin = $stage2.origin
	$stage3.startorigin = $stage3.origin
	$antenna_plat_origin.startorigin = $antenna_plat_origin.origin

	// Starts lowered (0=lowered, 1=raised, 2=moving)
	self.pos = 0

	self.raisingThread = NULL
	self.loweringThread =  NULL
	
	thread initSoundEntities

end

initSoundEntities:
	local.soundmaker = spawn "animate/military_radio.tik" "origin" $antenna_left_door_origin.origin "scale" "0.01" 
	local.soundmaker rendereffects "+dontdraw"
	local.soundmaker bind $antenna_left_door_origin
	$antenna_left_door_origin.soundmaker = local.soundmaker
	
	local.soundmaker = spawn "animate/military_radio.tik" "origin" $antenna_right_door_origin.origin "scale" "0.01" 
	local.soundmaker rendereffects "+dontdraw"
	local.soundmaker bind $antenna_right_door_origin
	$antenna_right_door_origin.soundmaker = local.soundmaker

	thread doorSoundStateThread
	
	local.soundmaker = spawn "animate/military_radio.tik" "origin" $antenna_origin.origin "scale" "0.01" 
	local.soundmaker bind $antenna_origin
	local.soundmaker rendereffects "+dontdraw"
	$stage2.soundmaker = local.soundmaker

	thread antennaSoundStateThread
end

antennaSoundStateThread:
	while (1)
	{
		if (level.antennaSoundState == 1)
		{
			$stage2.soundmaker stoploopsound
			$stage2.soundmaker playsound Antenna_start
			wait 0.1
			level.antennaSoundState = 2
		}
		else if (level.antennaSoundState == 2)
		{
			$stage2.soundmaker loopsound Antenna_up_loop
			level.antennaSoundState = 0
		}
		else if (level.antennaSoundState == 3)
		{
			$stage2.soundmaker stoploopsound
			$stage2.soundmaker playsound Antenna_stop
			level.antennaSoundState = 0
		}
		wait 0.1
	}
end

doorSoundStateThread:
	while (1)
	{
		if (level.antennaDoorSoundState == 1)
		{
			// Door open sound - start
			$antenna_right_door_origin.soundmaker playsound Antenna_doors_open_start
			$antenna_left_door_origin.soundmaker playsound Antenna_doors_open_start
			wait 0.1
			level.antennaDoorSoundState = 5
		}
		else if (level.antennaDoorSoundState == 2)
		{
			// Door open sound - finish
			$antenna_right_door_origin.soundmaker stoploopsound
			$antenna_right_door_origin.soundmaker playsound Antenna_doors_open_stop
			$antenna_left_door_origin.soundmaker stoploopsound
			$antenna_left_door_origin.soundmaker playsound Antenna_doors_open_stop
			level.antennaDoorSoundState = 6
		}
		else if (level.antennaDoorSoundState == 3)
		{
			// Door Close Sound - start
			$antenna_right_door_origin.soundmaker playsound Antenna_doors_close_start
			$antenna_left_door_origin.soundmaker playsound Antenna_doors_close_start
			wait 0.1
			level.antennaDoorSoundState = 5
		}
		else if (level.antennaDoorSoundState == 4)
		{
			// Door Close Sound - finish
			$antenna_right_door_origin.soundmaker stoploopsound
			$antenna_right_door_origin.soundmaker playsound Antenna_doors_close_stop
			$antenna_left_door_origin.soundmaker stoploopsound
			$antenna_left_door_origin.soundmaker playsound Antenna_doors_close_stop
			level.antennaDoorSoundState = 6
		}
		else if (level.antennaDoorSoundState == 5)
		{
			// Door move sound
			$antenna_right_door_origin.soundmaker loopsound Antenna_doors_move
			$antenna_left_door_origin.soundmaker loopsound Antenna_doors_move
			level.antennaDoorSoundState = 6
		}
		else
		{
			wait 0.1
		}
	}
end

openLeftDoor:
	level.antennaDoorSoundState = 1
	$antenna_left_door_origin time 4
	$antenna_left_door_origin rotatexdownto -85
	$antenna_left_door_origin waitmove
	level.antennaDoorSoundState = 2
end

openRightDoor:	
	$antenna_right_door_origin time 4
	$antenna_right_door_origin rotatexupto 85
	$antenna_right_door_origin waitmove
end

closeLeftDoor:
	level.antennaDoorSoundState = 3
	$antenna_left_door_origin time 4
	$antenna_left_door_origin rotatexupto 0
	$antenna_left_door_origin waitmove
	level.antennaDoorSoundState = 4
end

closeRightDoor:
	$antenna_right_door_origin time 4
	$antenna_right_door_origin rotatexdownto 0
	$antenna_right_door_origin waitmove
end

//--------------------------------------------------------------
//Antenna manipulation code
//--------------------------------------------------------------
engage_antenna:

	self.raisingThread = local
	if (self.loweringThread) 
	{
		self.loweringThread delete
		self.loweringThread = NULL
	}

	// Moving (0=lowered, 1=raised, 2=moving)
	self.pos = 2

	$antenna_handle_origin time 1
	$antenna_handle_origin rotatezupto 85
	$antenna_handle_origin waitmove
	$antenna_handle_origin playsound switchbox 
	
	//open doors

	thread openLeftDoor
	waitthread openRightDoor

	level.antennaSoundState = 1

	//move antenna
	$antenna_plat_origin speed 30
	local.move = $antenna_plat_origin.startorigin + (0 0 257)
	$antenna_plat_origin moveto local.move
	$antenna_plat_origin waitmove

	level.antennaSoundState = 1

	$stage2 notsolid
	$stage2 speed 20
	local.move = $stage2.startorigin + (0 0 144)
	$stage2 moveto local.move
	$stage2 waitmove
	$stage2 solid

	level.antennaSoundState = 1
	
	$stage3 notsolid
	$stage3 speed 10
	local.move = $stage3.startorigin + (0 0 250)
	$stage3 moveto local.move
	$stage3 waitmove
	$stage3 solid

	level.antennaSoundState = 3

	// Raised (0=lowered, 1=raised, 2=moving)
	self.pos = 1
end

lower_antenna:
	self.loweringThread = local
	if (self.raisingThread) 
	{
		self.raisingThread delete
		self.raisingThread = NULL
	}

	// Moving (0=lowered, 1=raised, 2=moving)
	self.pos = 2

	$antenna_handle_origin time 1
	$antenna_handle_origin rotatezdownto 5
	$antenna_handle_origin waitmove
	$antenna_handle_origin playsound switchbox 
	
	level.antennaSoundState = 1

	//move antenna
	$stage3 notsolid
	$stage3 speed 10
	$stage3 moveto $stage3.startorigin
	$stage3 waitmove
	$stage3 solid

	level.antennaSoundState = 1

	$stage2 notsolid
	$stage2 speed 20
	$stage2 moveto $stage2.startorigin
	$stage2 waitmove
	$stage2 solid

	level.antennaSoundState = 1

	$antenna_plat_origin speed 30
	$antenna_plat_origin moveto $antenna_plat_origin.startorigin
	$antenna_plat_origin waitmove

	level.antennaSoundState = 3
	
	//open doors
	thread closeLeftDoor
	thread closeRightDoor

	// Lowered (0=lowered, 1=raised, 2=moving)
	self.pos = 0
end





//--------------------------------------------------------------
//Placeholder cinematics for the goosestepping parade (Axis victory)
//--------------------------------------------------------------
goosestep_start:

	local.drunk = spawn human/german_wehrmact_soldier.tik
 	local.bottle = spawn script_model model models/miscobj/schnaps.tik
	local.drunk.origin = ( -1809 1235 -95 )
	local.bottle.angles = ( 0 0 180 )
	local.drunk exec global/disable_ai.scr
	local.drunk.angle = 5
	local.drunk holster
 	local.bottle attach local.drunk tag_weapon_right 1 ( -1 -3 5 )

	local.drunk2 = spawn human/It_AX_Ital_Vol.tik
 	local.bottle2 = spawn script_model model models/miscobj/schnaps.tik
	local.bottle2.angles = ( 0 0 180 )
	local.drunk2.origin = ( -1642 1139 -95 )
	local.drunk2.angle = 190
	local.drunk2 holster
 	local.drunk2 exec global/disable_ai.scr
 	local.bottle2 attach local.drunk2 tag_weapon_left 1 ( -1 -3 5 )
	local.drunk2 anim chatter03

	local.drunk3 = spawn human/It_AX_Ital_Vol.tik
	local.drunk3.origin = ( -1685 1267 -95 )
	local.drunk3.angle = 202
	local.drunk3 holster
 	local.drunk3 exec global/disable_ai.scr
	local.drunk3 anim chatter02

	local.drunk4 = spawn human/It_AX_Ital_Vol.tik
	local.drunk4.origin = ( -1744 1335 -95 )
	local.drunk4.angle = 230
	local.drunk4 holster
 	local.drunk4 exec global/disable_ai.scr
	local.drunk4 anim chatter01

	local.drunk lookat local.drunk2	
	local.drunk2 lookat local.drunk	
	local.drunk3 lookat local.drunk	
	local.drunk4 lookat local.drunk	

	local.camera = spawn func_camera spawnflags 24
	local.camera fov 40 2
	local.camera speed 0.5
	local.camera movetopos ( -1340 895 247 )
	local.camera lookat local.drunk

	cuecamera local.camera

	wait 0.8
	local.drunk anim handler_discipline
	local.drunk2 anim generic_stand_bored_b

	wait 2.0
	local.drunk4 anim 00A001_victory

	wait 0.5
	local.drunk lookat local.drunk4	
	local.drunk2 anim 00A001_victory
	wait 0.5
	local.drunk3 anim 00A001_victory
	wait 1.0


end






