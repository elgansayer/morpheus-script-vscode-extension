main:

	level.music="e1l3"
	fadeout .001 0 0 0 1
	wait 0.5
	drawhud 0
	level waittill spawn
	drawhud 0

	for (local.i = 1; local.i <= $BoatEnemy.size; local.i++) {
		$BoatEnemy[local.i].enemyname = "BoatEnemy"
	}

	exec global/auto.scr "e1l3"
	println "-----------------------------------------------"
	println "-----------------------------------------------"
	println "-----------------------------------------------"
	println "E1L3"
	println "-----------------------------------------------"

	thread MusicStart

	waitexec maps/e1l3/hacks.scr
	exec maps/e1l3/aicleanup.scr

	level waittill prespawn
	level waittill spawn

	level.aipronechance = 18
	level.aicrouchchance = 28
	level.nodrophealth = 1
	
	level.enemyhealthscalar = 0.7
	
	thread InitLevel
	thread InitPlayer

	//wait 1

/*

Sections:

	"TankRide" - Tank ride in
	"Briefing" - Briefing with Cap'n
	"Boating" - Boat section (guys set up boats, player gets in a boat, boat travels across, other boat gets sunk)
	"JailBreak" - Liberate Brit prisoners
	"Conquest" - Take fortress with Brits
	"Sneakers" - Meet Klaus, sneak through streets
	"RoofJaunt" - Germans are alerted, player and Klaus escape to roof, hop from roof to roof, then
						Klaus picks a lock while player covers him
	"FinalEscape" - Klaus and player get into uniform, walk out and show papers to Germans, get in truck,
						and drive off into the sunset

*/
	$fanblade1 bind $center
	$fanblade2 bind $center
	$fanblade3 bind $center
	$fanblade4 bind $center  
	$center rotatey -115


	waitthread maps/e1l3/Objectives.scr::start_init

	waitthread maps/e1l3/TankRide.scr::init
	waitthread maps/e1l3/Briefing.scr::init
	waitthread maps/e1l3/Boating.scr::init
	waitthread maps/e1l3/JailBreak.scr::init
	waitthread maps/e1l3/Conquest.scr::init
	waitthread maps/e1l3/Sneakers.scr::init
	waitthread maps/e1l3/RoofJaunt.scr::init
	waitthread maps/e1l3/FinalEscape.scr::init
	waitthread maps/e1l3/Courtyard.scr::init

	waitthread maps/e1l3/Objectives.scr::finish_init

	if (level.warpTo == "None") {
		wait 2
		fadein 5.5 0 0 0 1
	}
	else {
		fadein 0.001 0 0 0 1
		drawhud 1
  		$player item weapons/Uk_W_Vickers.tik
		$player use vickers
	}

	thread InitAmbientTriggers

end


InitAmbientTriggers:

	if (!$exterior)
		end

	local.special_trig = NULL
	for (local.i=1; local.i<$exterior.size; local.i++)
	{
		if ($exterior[local.i].exiting_fort_gate == 1)
		{
			local.special_trig = $exterior[local.i]
			break
		}
	}

	if (!local.special_trig)
		end

	local.special_trig waittill trigger

	// now go through and change all #set 2's to #set 3's

	for (local.i=1; local.i<$exterior.size; local.i++)
	{
		if ($exterior[local.i].set == 2)
			$exterior[local.i].set = 3
	}

end


// ------------------------------------------------
// ------------------------------------------------
InitLevel:

	$player heal 1
	setcvar g_mission 1

//	waitthread Fadein
end


// ------------------------------------------------
// ------------------------------------------------
InitPlayer:

	level waittill prespawn
	level waittill spawn
	wait 1.0
	waitexec global/loadout.scr

end


// ------------------------------------------------
// ------------------------------------------------
Fadein:
	level.startangles = $player.viewangles
	$player physics_off
	exec global/fadein.scr
	waitframe
	waitthread global/fadein.scr::DoFadeIn e1l3_1 e1l3_2 e1l3_3 NIL 1
	$player.viewangles = level.startangles  //undo any mousemovment during the hud stuff
end

Autosave1:
	//dprintln "AUTO:  Doing autosave 1"
	waitexec global/autosave.scr 1

end


Autosave2:
	//dprintln "AUTO:  Doing autosave 2"
	waitexec global/autosave.scr 2

end

Autosave3:
	//dprintln "AUTO:  Doing autosave 3"
	waitexec global/autosave.scr 3

end

//Autosave4:
	//dprintln "AUTO:  Doing autosave 4"
//	waitexec global/autosave.scr 4

//end

Autosave5:
	//dprintln "AUTO:  Doing autosave 5"
	waitexec global/autosave.scr 5

end



////////////////////////////////////////////////
// You Make Me Sick I Make Music

MusicStart:
	thread global/music.scr::PlaySongLoop "sound/music/medicdance.mp3" 1.0
end

MusicMeetingClaus:
	waitthread global/music.scr::StopSong 2.0
	thread global/music.scr::PlaySongLoop "sound/music/Monte_Cassino.mp3" 1.0
end

MusicEscaping:
	waitthread global/music.scr::StopSong 2.0
	thread global/music.scr::PlaySongLoop "sound/music/mus_06a_mystery.mp3" 1.0
end


