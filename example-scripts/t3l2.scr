main:
	level.script = "maps/t3l2.scr"
	level.music = "t3l2"

	exec global/auto.scr
	$world farplane 20000
	$world skybox_farplane 5000


level waittill prespawn

	level.skill = getcvar (skill)
	level.cantgothatway = 0
	level.bombnum = 0
	level.tempnum = 0
	level.sightdistance = 2048


	level.enemytankspeed = 200
	level.halftrakspeed = 300
	level.lookahead = 256
	level.playertanktarget = $player
	level.flyplane = 1
	level.aipronechance = 0
	
	level.fastspeed = 300
	level.slowspeed = 160

	level.dontdropweapons = 1
	level.nodrophealth = 1

	$playertank waitthread global/vehicles_thinkers.scr::players_tank $playertank_trigger
	$playertank vehiclespeed level.slowspeed
	level.currently_fast = 0

	$player physics_off
	//$player stufftext "ui_hud 0"
	drawhud 0
	//*** fade out
	fadeout .001 0 0 0 1
//	drawhud 2

	$player takeall


level waittill spawn

	$world farclipoverride 5000
	
	$player takeall
	$player holster
	$new_fire anim stop

	thread global/friendly.scr::friendlygen

	exec gags/T3L2_KingTiger.scr
	exec gags/t3l2_german_stickybomber.scr

	setcvar g_mission 3
	$player fullheal

	thread sky_smoke
	$rubblesmoke anim start

	level.bridge_ready = 0

	$flak88_1	thread global/vehicles_thinkers.scr::enemy_flak_think
	$flak88_2	thread global/vehicles_thinkers.scr::enemy_flak_think
	$flak88_3	thread global/vehicles_thinkers.scr::enemy_flak_think
	$flak88_4	thread global/vehicles_thinkers.scr::enemy_flak_think
	$finalopeltruck hide
	$finalopeltruck notsolid
	thread global/spotlight.scr::deadcorona $finalopeltruck "light left" 
	thread global/spotlight.scr::deadcorona $finalopeltruck "light right"
	$s5_tank1 thread gags/T3L2_KingTiger.scr::HideTank
	$s5_tank1 immune aagun
	$s10_tank1 thread gags/T3L2_KingTiger.scr::HideTank
	$s10_tank1 immune aagun
	$s15_tank1 thread gags/T3L2_KingTiger.scr::HideTank
	$s20_tank1 thread gags/T3L2_KingTiger.scr::HideTank
	$s20_tank1 immune aagun
	$s25_tank1 thread gags/T3L2_KingTiger.scr::HideTank
	$s25_tank1 immune aagun
	$s30_tank1 thread gags/T3L2_KingTiger.scr::HideTank
	$s35_tank1 thread gags/T3L2_KingTiger.scr::HideTank
	$s35_tank1 immune aagun
	$s40_tank1 thread gags/T3L2_KingTiger.scr::HideTank
	$s40_tank1 immune aagun
	$s43_tank1 thread gags/T3L2_KingTiger.scr::HideTank	
	$s45_tank1	thread global/vehicles_thinkers.scr::enemy_tank_think 0 empty_tiger
	$s45_tank1.thinkthread = parm.previousthread
	$s45_tank1 nodamage	
	$s50_tank1 thread gags/T3L2_KingTiger.scr::HideTank
	$s50_tank1 immune aagun
	$t34_friendly1 thread gags/T3L2_KingTiger.scr::HideTank
	$t34_friendly2 thread gags/T3L2_KingTiger.scr::HideTank
	$friendlyopel hide
	$friendlyopel notsolid
	$friendlyopel nodamage
	thread global/spotlight.scr::deadcorona $friendlyopel "light left" 
	thread global/spotlight.scr::deadcorona $friendlyopel "light right"
	$obstructiontank thread gags/T3L2_KingTiger.scr::HideTank
	$finalbomber hide
	$stuka hide
	$yak hide
	
    	for(local.i = 1;local.i <= $poles.size; local.i ++)
        {
        	local.bleh = $poles[local.i].target
        	local.bleh notsolid
        }

		// turn off our friendly ai....
	for (local.i=1;local.i<=$friendly.size;local.i++)
	{
		$friendly[local.i] rendereffects "+dontdraw"
		$friendly[local.i] ai_off
	}


	thread trucksetup
	thread global/bomber.scr::bomb 4
	thread global/bomber.scr::bomb 5
	thread global/bomber.scr::bomb 6

//	$trippyguytrigger thread trippythread

	// initialize the objective list
	waitthread initobjectives

//thread spawnerleaderinit

	wait 0.5
	fadein 3.5 0 0 0 1
	wait 1.0
	$player physics_on
	$player stufftext "tmstop; tmstartloop sound/music/mus_Schmerzen_01a.mp3"
	drawhud 2

	local.usekey = getboundkey1 "+attacksecondary"
	local.msg = (loc_convert_string "Press ") + local.usekey + (loc_convert_string " to control DTM machine gun.")
	thread gags/t1l1_barn_crash.scr::throbtext local.msg 7

end
 
 
settletank:
	wait 1.0
	$s45_tank1 lockmovement
end

endleveltrigger:
	$player holster
	waitexec global/award_medal.scr 3
	setcvar "g_mission" "3"
	missiontransition briefing/briefingd1 1	// 1 means skip fade...
end

end

//----------------------------------------------------------------------
DoFinalOpelTruck:
//
//	Show the truck and have it drive on its way...
//----------------------------------------------------------------------
	self show
	self solid

	if (level.skill == 0)
		local.num_guys = 1
	else if (level.skill == 1)
		local.num_guys = 1
	else
		local.num_guys = 2

	self waitthread gags/t3l1_enemyspawn.scr::DoTruck 2500 local.num_guys 0

	// give them guns...
	for (local.i=1;local.i<=self.passenger_count;local.i++)
	{
		if ( self.passenger[local.i]!=NIL )
		{
			self.passenger[local.i] gun "Mauser KAR 98K"
		}
	}

	// make truck go and wait until it's done moving...
	self waitthread gags/t3l1_enemyspawn.scr::TruckDrive $finalopeltruck_path 0 level.enemytankspeed 0 0 0

	// when we get there, unload our passengers and have them go where they're supposed to go...
	// turn off our lights..
	self vehicleanim idlenolights

	for (local.i=1;local.i<=self.passenger_count;local.i++)
	{
		if ( self.passenger[local.i]!=NIL )
		{
			local.name = "opeltruckenemyanchor" + local.i
			self.passenger[local.i] thread GoToPosition local.name
		}
	}

	while ( self.driver.done_unloading!=1 )
	{
		wait 0.2
	}

	// don't need this guy...
	self.driver delete


	end

//----------------------------------------------------------------------
GoToPosition local.pos:
//----------------------------------------------------------------------

	while ( self.done_unloading!=1 )
	{
		waitframe
	}

	// make sure we don't get distracted..
	self exec global/disable_ai.scr

	self runto local.pos
	self waittill movedone

	// turn on our AI and hopefully they'll start attacking...
	self exec global/enable_ai.scr

	end


//----------------------------------------------------------------------
finalopeltruck:
//----------------------------------------------------------------------
	//called by the BSP
	//Opel truck drives to infantry spawn location
	$finalopeltruck thread DoFinalOpelTruck

	thread gags/T3L2_KingTiger.scr::BridgeTankGo
	wait 3
	thread gags/T3L2_KingTiger.scr::BridgeTank2Go
	thread gags/T3L2_KingTiger.scr::DoBridgeSpawns
	end

//----------------------------------------------------------------------
T34friends:
//----------------------------------------------------------------------
	$t34_friendly2 thread gags/T3L2_KingTiger.scr::ShowTank
	thread friendlyT34pair
	end

//----------------------------------------------------------------------
s5:
//----------------------------------------------------------------------
	//called by the BSP
	//one enemy tank comes around corner by the start and attack
	thread ai_spawn_900
	$s5_tank1 thread gags/T3L2_KingTiger.scr::ShowTank
	$s5_tank1	thread global/vehicles_thinkers.scr::enemy_tank_think 0 empty_panzer_desert
	if (isAlive $s5_tank1)
		$s5_tank1 thread drive_path $s5_tank_path level.enemytankspeed
	end
//----------------------------------------------------------------------
s10:
	//called by the BSP
	//one enemy tank comes around corner by the start and attack
//----------------------------------------------------------------------
	$s10_tank1 thread gags/T3L2_KingTiger.scr::ShowTank
	$s10_tank1	thread global/vehicles_thinkers.scr::enemy_tank_think 0	empty_panzer_desert
	if (isAlive $s10_tank1)		
		$s10_tank1 thread drive_path $s10_tank_path level.enemytankspeed
	end

//----------------------------------------------------------------------
s15:
//----------------------------------------------------------------------
	//called by the BSP
	//one enemy tank comes around corner by the start and attack
	$s15_tank1 thread gags/T3L2_KingTiger.scr::ShowTank
	wait 2
	$s15_tank1	thread global/vehicles_thinkers.scr::enemy_tank_think 0 panzerwerfer
	if (isAlive $s15_tank1)		
		$s15_tank1 thread drive_path $s15_tank1_path level.enemytankspeed
	$s20_tank1 thread gags/T3L2_KingTiger.scr::ShowTank
	wait 5
	$s20_tank1	thread global/vehicles_thinkers.scr::enemy_tank_think 0 empty_panzer_desert
	if (isAlive $s20_tank1)		
		$s20_tank1 thread drive_path $s20_tank1_path level.enemytankspeed
	end

//----------------------------------------------------------------------
s25:
//	called by the BSP
//	Start up tank thinking
//	autosave position 1
//----------------------------------------------------------------------
	$s25_tank1 thread gags/T3L2_KingTiger.scr::ShowTank
	$s25_tank1	thread global/vehicles_thinkers.scr::enemy_tank_think 0 empty_panzer_desert

	end


//----------------------------------------------------------------------
s30:
//	called by the BSP
//	two enemy tanks come around by the exit and attack
//----------------------------------------------------------------------
	$s30_tank1 thread gags/T3L2_KingTiger.scr::ShowTank
	$s30_tank1	thread global/vehicles_thinkers.scr::enemy_tank_think 0 panzerwerfer
	if (isAlive $s30_tank1)	
		$s30_tank1 thread drive_path $s30_tank_path level.enemytankspeed
	end

//----------------------------------------------------------------------
s35:
	//called by the BSP
	//two enemy tanks come around by the exit and attack
//----------------------------------------------------------------------
	$s35_tank1 thread gags/T3L2_KingTiger.scr::ShowTank
	$s35_tank1 thread global/vehicles_thinkers.scr::enemy_tank_think 0 empty_panzer_desert
	if (isAlive $s35_tank1)	
		$s35_tank1 thread drive_path $s35_tank_path level.enemytankspeed
	end

//----------------------------------------------------------------------
s40:
	//called by the BSP
	//two enemy tanks come around by the exit and attack
	// autosave position 2
//----------------------------------------------------------------------
	exec global/autosave.scr 1
	$s40_tank1 thread gags/T3L2_KingTiger.scr::ShowTank
	$s40_tank1	thread global/vehicles_thinkers.scr::enemy_tank_think 0 empty_panzer_desert
	if (isAlive $s40_tank1)	
		$s40_tank1 thread drive_path $s40_tank_path level.enemytankspeed
	end

//----------------------------------------------------------------------
s43:
	//called by the BSP
	//two enemy tanks come around by the exit and attack
//----------------------------------------------------------------------
	$s43_tank1 thread gags/T3L2_KingTiger.scr::ShowTank
	$s43_tank1 thread global/vehicles_thinkers.scr::enemy_tank_think 0 panzerwerfer
	$s43_tank1 thread drive_path $s43_tank_path level.enemytankspeed

	end

//----------------------------------------------------------------------
ai_spawn_900:
//----------------------------------------------------------------------
waitthread global/ai.scr::spawnset 900 set900
$set900 thread runto_target_delay 3
end

//----------------------------------------------------------------------
runto_target_delay local.delay:
//----------------------------------------------------------------------

self exec global/disable_ai.scr
self thread enable_ai local.delay
self runto self.target

end

//----------------------------------------------------------------------
enable_ai local.delay:
//----------------------------------------------------------------------

wait local.delay
self exec global/enable_ai.scr
wait 2
self attackplayer
end

//----------------------------------------------------------------------
ai_spawn_1000:
//----------------------------------------------------------------------
thread global/ai.scr::spawnset 1000 set1000
end

//----------------------------------------------------------------------
ai_spawn_1001:
//----------------------------------------------------------------------
thread global/ai.scr::spawnset 1001 set1001
end

//----------------------------------------------------------------------
ai_spawn_1002:
//----------------------------------------------------------------------
thread global/ai.scr::spawnset 1002 set1002
thread global/ai.scr::spawnset 1102 buildingdeath
end

//----------------------------------------------------------------------
DeleteTank:
//----------------------------------------------------------------------

if ( self.gun )
	self.gun delete

if ( self.gun2 )
	self.gun2 delete

if ( self.gun3 )
	self.gun3 delete

	self delete

end

//----------------------------------------------------------------------
ai_spawn_1003:
	//one enemy tank comes around corner by the start and attack
//----------------------------------------------------------------------

	$new_fire anim start
	// get rid of stuff we don't need anymore...
	$s5_tank1 thread DeleteTank
	$s10_tank1 thread DeleteTank
	$s15_tank1 thread DeleteTank
	$s20_tank1 thread DeleteTank
	$s25_tank1 thread DeleteTank
	$s30_tank1 thread DeleteTank
	$stuka delete

	if ( $flak88_1!=NULL && $flak88_1!=NIL )
		$flak88_1 delete

	// delete those pesky germans we don't need anymore...
	thread global/ai.scr::DeleteEnemySpawner $set1000 1000
	thread global/ai.scr::DeleteEnemySpawner $set1001 1001
	thread global/ai.scr::DeleteEnemySpawner $set1002 1002


	$obstructiontank thread gags/T3L2_KingTiger.scr::ShowTank
	$obstructiontank thread global/vehicles_thinkers.scr::enemy_tank_think 0 empty_panzer_desert
	if (isAlive $obstructiontank)		
	{
		$obstructiontank thread drive_path $obstructiontank_path level.enemytankspeed
		$obstructiontank thread DontKillMe
	}
	thread global/ai.scr::spawnset 1003	set1003
end

//----------------------------------------------------------------------
DontKillMe:
//----------------------------------------------------------------------
	// wait enuf time for enemy_tank_think to set my hitpoints...
	wait 0.25
	self nodamage
	end

//----------------------------------------------------------------------
ai_spawn_1004:
//----------------------------------------------------------------------
thread global/ai.scr::spawnset 1004 set1004
end

//----------------------------------------------------------------------
ai_spawn_1005:
//----------------------------------------------------------------------
$fire_smoke_1 anim stop
thread global/ai.scr::spawnset 1005 set1005
end

//----------------------------------------------------------------------
bridgefloorcollapse:
//----------------------------------------------------------------------
	$t34_friendly1 thread gags/T3L2_KingTiger.scr::ShowTank
	$s45_tank1 notsolid
	$s45_tank1 thread tankfall
	// autosave position 3
	wait 5
	exec global/autosave.scr 2
end

//----------------------------------------------------------------------
tankfall:
//----------------------------------------------------------------------
	if (isAlive self)	
	{
		if (self.thinkthread)
			self.thinkthread delete
		
		if (self.attack_thread)
			self.attack_thread delete

		if (self.pain_thread)
			self.pain_thread delete
		
		//self lockmovement
		self moveanim 32P002_falloff
		wait 4
		thread global/exploder.scr::explode 352
		self waittill animdone

		//wait 5.0
		$s45_tank1 thread gags/T3L2_KingTiger.scr::HideTank	
		self remove
	}
end


//----------------------------------------------------------------------
s50:
	//called by the BSP
	//two enemy tanks come around by the exit and attack
//----------------------------------------------------------------------
	thread global/ai.scr::spawn 1006
	thread removehealth
	$s50_tank1 thread gags/T3L2_KingTiger.scr::ShowTank
	$s50_tank1	thread global/vehicles_thinkers.scr::enemy_tank_think 0 empty_panzer_desert
	if (isAlive $s50_tank1)	
		$s50_tank1 thread drive_path $s50_tank_path level.enemytankspeed


	for (local.i=1;local.i<=$friendly.size;local.i++)
	{
		$friendly[local.i] rendereffects "-dontdraw"
		$friendly[local.i] ai_on
	}

	end

//----------------------------------------------------------------------
friendlyT34pair:
//----------------------------------------------------------------------
	$t34_friendly1 nodamage
	$t34_friendly2 nodamage



	$t34_friendly1 thread global/FriendlyTank.Scr::DrivePath $t34_friendly1_path 100 4096 0
	wait 1
	$t34_friendly2 thread global/FriendlyTank.Scr::DrivePath $t34_friendly2_path 100 4096 0
	wait 3

	//called by the BSP
	//Opel truck drives to infantry spawn location

	$friendlyopel thread DoFriendlyTruck

	$t34_friendly1 thread global/FriendlyTank.Scr::FriendlyTankStart $friendlytarget empty_t34
	wait 2
	$t34_friendly2 thread global/FriendlyTank.Scr::FriendlyTankStart $friendlytarget empty_t34

	wait 5

	$t34_friendly1.type = empty_t34
	$t34_friendly2.type = empty_t34

	$t34_friendly1 takedamage
	$t34_friendly2 takedamage

	end

//----------------------------------------------------------------------
DoFriendlyTruck:
//----------------------------------------------------------------------
	self show
	self solid
	self.collisionent=$opel_mask

	self waitthread gags/t3l1_enemyspawn.scr::DoTruck 1500 1 0 NIL 1
	self nodamage
	// give guys weapons

	self.passenger[1] gun "nagant rifle"
//	self.passenger[2] gun "nagant rifle"

	self waitthread gags/t3l1_enemyspawn.scr::TruckDrive $friendlyopel_path 0 level.enemytankspeed 0 0 0

	self.passenger[1] thread GoToPosition $anchor6
//	self.passenger[2] thread GoToPosition $anchor7

// don't want him running around with that little gun...
	self.driver gun "nagant rifle"
	self.driver thread GotoPosition $anchor8


	end


//----------------------------------------------------------------------
kablam1:
//----------------------------------------------------------------------
	thread global/exploder.scr::explode 5
	end

//----------------------------------------------------------------------
kablam2:
//----------------------------------------------------------------------
	exec gags/t3l2_german_stickybomber.scr::sticky_spawner_off
	thread global/exploder.scr::explode 5
	thread tankkiller
	thread global/exploder.scr::explode 199
	wait 3
	thread modifyobjective3
	waitthread captainmonroe_say7
//	wait 9
	thread endgamecamera
	end

//----------------------------------------------------------------------
kablamtank:
//----------------------------------------------------------------------
	thread global/exploder.scr::explode 5
	thread global/exploder.scr::explode 18
	thread global/exploder.scr::explode 21

	$obstructiontank takedamage
	$obstructiontank.health = 200
	radiusdamage $obstructiontank.origin 500 500 1
//	$obstructiontank thread global/vehicles_thinkers.scr::tank_killed

	end

//----------------------------------------------------------------------
tankkiller:
//----------------------------------------------------------------------
	$tankkiller volumedamage 1000
	end

//----------------------------------------------------------------------
buildingcrush:
//----------------------------------------------------------------------
	$building5_damage_volume volumedamage 1000
	end

//----------------------------------------------------------------------
buildingcrush1:
//----------------------------------------------------------------------
	$building1_damage_volume volumedamage 1000
	thread global/ai.scr::DeleteEnemySpawner $buildingdeath 1102
	end

//----------------------------------------------------------------------
speedup:
//----------------------------------------------------------------------
	if (level.currently_fast == 1)
		end
	for (local.speed = level.slowspeed; local.speed <= level.fastspeed; local.speed += 1)
	{
		$playertank vehiclespeed local.speed
		waitframe
	}
	level.currently_fast = 1
	end

//----------------------------------------------------------------------
slowdown:
//----------------------------------------------------------------------
	if (level.currently_fast != 1)
		end
	for (local.speed = level.fastspeed; local.speed >= level.slowspeed; local.speed -= 1)
	{
		$playertank vehiclespeed local.speed
		waitframe
	}
	level.currently_fast = 0
	end

//----------------------------------------------------------------------
trucksetup:
//----------------------------------------------------------------------
	$triggertruckleft thread truckleftside
	$triggertruckright thread truckrightside
end

//----------------------------------------------------------------------
truckleftside:
//----------------------------------------------------------------------
	self waittill trigger
	thread truckpush "left"
end

//----------------------------------------------------------------------
truckrightside:
//----------------------------------------------------------------------
	self waittill trigger
	thread truckpush "right"
end

//----------------------------------------------------------------------
truckpush local.side:
//----------------------------------------------------------------------
	if(level.truckpushed != 1)
	{
		level.truckpushed = 1
		if(local.side == "left")
		{
			$truck followpath $truckrotateleft
			$player playsound king_vehicle_crash
		}
		if(local.side == "right")
		{
			$truck followpath $truckrotateright
			$player playsound king_vehicle_crash
		}
		$truck waitmove
	}
end

//-------------------------------------------------------
fire_on:
//-------------------------------------------------------
	$new_fire anim start
end

//-------------------------------------------------------
fire_off:
//-------------------------------------------------------
	$new_fire anim stop
end

//----------------------------------------------------------------------
sky_smoke:
//----------------------------------------------------------------------
   	for(local.i = 1;local.i <= $skysmoke.size; local.i ++)
	{
        //	$skysmoke[local.i].origin = ($skysmoke[local.i].origin + ( 0 0 -64 ))
        	$skysmoke[local.i] forceactivate
        	$skysmoke[local.i] anim start
        	wait .2
	}
end

//----------------------------------------------------------------------
pole_fall:
//----------------------------------------------------------------------

	local.ent = self.target

	local.ent playsound lightpole_fall_start
	local.ent notsolid

	local.angles = (vector_toangles ($player.origin - self.origin))
	local.ent.angles = (0 (local.angles[1] + 90) 0)

	self remove

	if ( local.ent.fangle == NIL )
		local.fallangle = 80
	else
		local.fallangle = local.ent.fangle

	local.rotate = 25
	local.current = local.rotate
	local.time = 0.5

	while (local.current < local.fallangle)
	{
		local.ent time local.time
		local.ent rotatezdown local.rotate
		local.ent waitmove
		local.time = local.time * 0.5
		local.current += local.rotate
	}
	local.current -= local.rotate

	local.ent time local.time
	local.ent rotatezdown (local.fallangle - local.current)
	local.ent waitmove

	local.ent playsound lightpole_fall_end

end

//----------------------------------------------------------------------
tree_fall:
//----------------------------------------------------------------------

	local.ent = self.target

	local.ent playsound tree_fall_start
	local.ent notsolid

	local.angles = (vector_toangles ($player.origin - self.origin))
	local.ent.angles = (0 (local.angles[1] + 90) 0)

	self remove

	if ( local.ent.fangle == NIL )
		local.fallangle = 80
	else
		local.fallangle = local.ent.fangle

	local.rotate = 25
	local.current = local.rotate
	local.time = 0.5

	while (local.current < local.fallangle)
	{
		local.ent time local.time
		local.ent rotatezdown local.rotate
		local.ent waitmove
		local.time = local.time * 0.5
		local.current += local.rotate
	}
	local.current -= local.rotate

	local.ent time local.time
	local.ent rotatezdown (local.fallangle - local.current)
	local.ent waitmove

	local.ent playsound tree_fall_end

end

//----------------------------------------------------------------------
bench_smash:
//----------------------------------------------------------------------
	if(self.smashed != 1)
	{
        	self.smashed = 1
        	local.bench = $(self.target)
        	local.thing = spawn script_model model "animate/fx_cratedebris_0.tik"
        	local.thing2 = spawn script_model model "animate/fx_cratedebris_0.tik"
        	local.org = angles_toforward ((local.bench.angles) + ( 0 90 0 ))
        	local.org = ((local.org * 35) + ((local.bench.origin)) + ( 0 0 45 ))	
        	local.thing.origin = local.org
        	local.thing anim start
        	local.org = angles_toforward ((local.bench.angles) + ( 0 -90 0 ) )
        	local.org = ((local.org * 35) + ((local.bench.origin)) + ( 0 0 45 ) )
        	local.thing2.origin = local.org
        	local.thing2 anim start
		local.bench remove
	}
end

//----------------------------------------------------------------------
trippythread:
//----------------------------------------------------------------------
	if(self.target == NULL || self.target == NIL)
	{
		end
	}
	local.guy = $(self.target)
	local.guy forceactivate
	local.guy exec global/disable_ai.scr
	local.guy exec global/prone.scr
	local.guy.movedoneradius = 256
	self waittill trigger
	local.guy exec global/stand.scr 
     	
	local.tripspot = $(local.guy.target)
	local.tripspot droptofloor
	local.spot1 = spawn script_origin
	local.spot1.origin = local.tripspot.origin 
	
	local.guy exec global/runto.scr local.spot1
	local.guy waittill movedone
	

	local.guy anim tanktrip
	
     
	local.guy waittill animdone
	local.runspot = $(local.tripspot.target)
	local.runspot droptofloor
	local.guy exec global/runto.scr local.runspot
end


//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// OBJECTIVES <begin>
//
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// initialize the objective list
// - NOTE: there may not be a 1-to-1 correlation between objective thread names and objective numbers
// - Here is the correlation map:
//
//   Objective #:	runs thread:
//		1			objective1
//		2			objective2
//		3			objective3
//
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
initobjectives:

	waitthread global/objectives.scr::add_objectives 1 1 "Destroy the South Bridge" $beacon_bridge.origin
	waitthread global/objectives.scr::add_objectives 2 2 "Return to the Soviet Recon Group" $beacon_soviets.origin
	waitthread global/objectives.scr::add_objectives 3 1 "Defend the Bridge Until Air Support Arrives"

	// set initial objective
	waitthread global/objectives.scr::current_objectives 2

end

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// modify objectives to reflect new status
//
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
modifyobjective1:

	// enable objective
	waitthread global/objectives.scr::add_objectives 1 2
	waitthread global/objectives.scr::current_objectives 1

	level.bridge_ready = 1

end

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// thread that waits for objective 1 completion
//
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
objective1:

	waitthread global/objectives.scr::add_objectives 1 3
	waitthread global/objectives.scr::current_objectives 2
	thread objective2

end

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// thread that waits for objective 2 completion
//
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
objective2:
	$objective2trigger waittill trigger
	thread spawnerleaderinit
	thread captainmonroe_say3
	thread T34friends

	waitthread global/objectives.scr::add_objectives 2 3
	waitthread global/objectives.scr::add_objectives 3 2
	waitthread global/objectives.scr::current_objectives 3

end

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// modify objectives to reflect new status
//
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
modifyobjective3:

	waitthread global/objectives.scr::add_objectives 3 3
	// MJG: don't cue end of level based on this timing scheme
//	wait 26
//	thread endleveltrigger

end


//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// OBJECTIVES <end>
//
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


//----------------------------------------------------------------------
bridgecollapsetrigger:
//
// modified exploder trigger for bridge collapse
//
//----------------------------------------------------------------------

	if ( parm.owner == $player && level.bridge_ready==1)
	{
		local.set = self.set
		self remove
		wait 0.25
		thread global/exploder.scr::explode local.set
		wait 0.25
		thread bridgefloorcollapse
		wait 3.0
		thread objective1
	}

end


//----------------------------------------------------------------------
drive_path local.path local.speed local.remove:
//----------------------------------------------------------------------
// drive Vector position, speed, acceleration, reach_distance, look_ahead
//println "z:              " local.path
//println "z:              " local.speed
self.driving = 1
self drive local.path local.speed 30 200 level.lookahead
//self modifydrive level.fastspeed 50 level.lookahead
self waitTill drive
if (self)
{
	self stop
	self.driving = 0
	if (local.remove == "remove")
		self remove
}
end

get_random_entry local.entries:
	local.n = randomint (local.entries.size)
	//println "XXX:" local.n
	local.entry = 0
get_random_entry_loop:
	local.result = local.entries[local.entry]
	if (local.result == NIL)
	{
		local.entry++
		goto get_random_entry_loop
	}
	if (local.n)
	{
		local.n--
		local.entry++
		goto get_random_entry_loop
	}
	
	println "result: " local.result

local.entries[local.entry] = NIL
end local.result

///////////////////////////////////
// spawner leader 
///////////////////////////////////

spawnerleaderinit:
	exec gags/t3l2_german_stickybomber.scr::sticky_spawner_on
	// opel truck
	thread finalopeltruck
	thread comrad_init

	$s35_tank1 thread DeleteTank
	$s40_tank1 thread DeleteTank
	$s43_tank1 thread DeleteTank
	$kingtiger1 thread DeleteTank
	$obstructiontank thread DeleteTank
	$flak88_2 delete
	$crashedstuka remove

	thread global/ai.scr::DeleteEnemySpawner $set1003 1003
	thread global/ai.scr::DeleteEnemySpawner $set1004 1004
	thread global/ai.scr::DeleteEnemySpawner $set1005 1005

end



///////////////////////////////////////////////////////////////////
// respawn friendly guys
///////////////////////////////////////////////////////////////////
// 
// all comrads must have a $targetname of "friendly"
// they need spawn origins use info_path_node with a $targetname "newfriendly1...10"
// they also need info_path_node with $targetname of $anchor1... 10
//

//----------------------------------------------------------------------
comrad_init:
//----------------------------------------------------------------------
	local.count = 0

	dprintln "COMRAD INIT!"
	for (local.i=1;local.i<$friendly.size+1;local.i++)
	{
		level.friendly[local.i].accuracy = 10
		level.friendly[local.i].sight = 5500
		level.friendly[local.i].hearing = 5500
		level.friendly[local.i].maxdist = 128
		level.friendly[local.i].mindist = 128
////		level.friendly[local.i] exec global/disable_ai.scr
		
		local.anchornumber = level.friendly[local.i].set
		
		level.friendly[local.i] leash 64
			
		if (local.anchornumber == 1)
			level.friendly[local.i] tether $anchor1
		else if (local.anchornumber == 2)
			level.friendly[local.i] tether $anchor2
		else if (local.anchornumber == 3)
			level.friendly[local.i] tether $anchor3
		else if (local.anchornumber == 4)
			level.friendly[local.i] tether $anchor4
		else if (local.anchornumber == 5)
			level.friendly[local.i] tether $anchor5
		else if (local.anchornumber == 6)
			level.friendly[local.i] tether $anchor6
		else if (local.anchornumber == 7)
			level.friendly[local.i] tether $anchor7
		else if (local.anchornumber == 8)
			level.friendly[local.i] tether $anchor8
		else if (local.anchornumber == 9)
			level.friendly[local.i] tether $anchor9
		else if (local.anchornumber == 10)
			level.friendly[local.i] tether $anchor10
		else
			println ( "oops no anchor for this one " )

		level.friendly[local.i] thread death_check_comrad
	}
	
end

//-------------------------------------------------------------------
comrad_runto local.ent:
//
//	Makes sure we actually get there...
//-------------------------------------------------------------------

	if ( self.runto_thread!=NIL && self.runto_thread!=NULL )
	{
		self.runto_thread delete
	}

	self.runto_thread	= local
	self.movedoneradius	= 6
	self.distance = 6

	self runto local.ent
	self waittill movedone

	while ( parm.movedone!=1 )
	{
		self runto local.ent
		self waittill movedone
	}

	self.runto_thread	= NULL
	self.movedoneradius	= 250
	self.distance = 250


	End

//----------------------------------------------------------------------
death_check_comrad:
//----------------------------------------------------------------------
	
	self waittill death

	local.rval = randomint(100)

	local.guy = spawn models/human/soviet_infantry1 "$targetname" "friendly" "type_attack" "cover"

	local.guy.accuracy = 10
	local.guy.sight = 5500
	local.guy.hearing = 5500
	local.guy.maxdist = 64
	local.guy.mindist = 64

	local.guy.group = self.group
	local.guy.set = self.set
	local.guy.anchored = 0


	local.guy thread death_check_comrad

//	wait 0.3
	local.guy exec global/disable_ai.scr
	
	if (self.set == 1)
	{
		local.guy.origin = $newfriendly1.origin
		local.guy waitthread comrad_runto $anchor1	
	}
	else if (self.set == 2)
	{
		local.guy.origin = $newfriendly2.origin
		local.guy waitthread $anchor2
	}
	else if (self.set == 3)
	{
		local.guy.origin = $newfriendly3.origin
		local.guy waitthread $anchor3
	}
	else if (self.set == 4)
	{
		local.guy.origin = $newfriendly4.origin
		local.guy waitthread $anchor4
	}
	else if (self.set == 5)
	{
		local.guy.origin = $newfriendly5.origin
		local.guy waitthread $anchor5
	}
	else if (self.set == 6)
	{
		local.guy.origin = $newfriendly6.origin
		local.guy waitthread $anchor6
	}
	else if (self.set == 7)
	{
		local.guy.origin = $newfriendly7.origin
		local.guy waitthread $anchor7
	}
	else if (self.set == 8)
	{
		local.guy.origin = $newfriendly8.origin
		local.guy waitthread $anchor8
	}
	else if (self.set == 9)
	{
		local.guy.origin = $newfriendly9.origin
		local.guy waitthread $anchor9
	}
	else if (self.set == 10)
	{
		local.guy.origin = $newfriendly10.origin
		local.guy waitthread $anchor10
	}
	else
		println ( " oops in death_check_comrad " )
	
	local.guy exec global/enable_ai.scr	

end


///////////////////////////////////////////////////////////////////
// captain monroe
///////////////////////////////////////////////////////////////////

//----------------------------------------------------------------------
captainmonroe_say1:
//----------------------------------------------------------------------

// "Sergeant Barnes, do you read me?  This is Captain Monroe.
// If you’re reading me, we need you to destroy the far bridge.
// Repeat.  Destroy the far bridge." 
waitthread global/items.scr::add_item "radio" nomessage
$player playsound dfr_T3L2_AC4501 wait
$player waittill sounddone

// "There’s a Panzer division trying to flank our position, and the only way
// we can hold them back is to drop that span into the river.  Out." 

$player playsound dfr_T3L2_AC4502 wait
$player waittill sounddone
waitthread global/items.scr::remove_item "radio" nomessage
	thread modifyobjective1
end

//----------------------------------------------------------------------
captainmonroe_say3: 
//----------------------------------------------------------------------

// "Sergeant Barnes, listen up." 

waitthread global/items.scr::add_item "radio" nomessage
$player playsound dfr_T3L2_AC4503 wait
$player waittill sounddone

// "Those tanks are sitting ducks from the air, but you must
// engage the enemy and hold that line until our bombers reach your position." 

$player playsound dfr_T3L2_AC4504 wait
$player waittill sounddone

// "ETA is two minutes.  Give 'em hell son. Out." 

$player playsound dfr_T3L2_AC4505 wait
$player waittill sounddone
waitthread global/items.scr::remove_item "radio" nomessage
end 

 
//----------------------------------------------------------------------
captainmonroe_say7:
//----------------------------------------------------------------------

// "Solid in the trenches, Barnes. The CP is anxiously awaiting for those documents." 
$player playsound dfr_T3L2_AC4506 wait
$player waittill sounddone
// "Get back here ASAP.  Godspeed, Sergeant. Over and Out." 

waitthread global/items.scr::add_item "radio" nomessage
$player playsound dfr_T3L2_AC4507 wait
$player waittill sounddone
waitthread global/items.scr::remove_item "radio" nomessage
end

//----------------------------------------------------------------------
endgamecamera:	
//----------------------------------------------------------------------
	//$player stufftext "ui_hud 0"	
	drawhud 0

	//MAC 9/08/02 - Must call this to stop looping music, or music at end mission screen will not play.
	$player stufftext "tmstop"
	
	forcemusic aux2 aux2
	cuecamera $watch_camera	
	$watch_camera watch $playertank 2
	$watch_camera cut
	wait 5
//	cuecamera $watch_camera
	$pe8_fleet anim start
	$finalbomber thread plane_fly
	$watch_camera watch $finalbomber 2
	$watch_camera cut
	wait 7
	fadeout 5 0 0 0 1
//	wait 24

	// MJG: wait until the fade to black is finished, then cue back to player and end level
	wait 5
	cueplayer
	thread endleveltrigger
end

//----------------------------------------------------------------------
plane_fly:
//----------------------------------------------------------------------
$finalbomber show
self followpath $plane_path 1500 2000 256
self playsound plane
self waitmove

end

//----------------------------------------------------------------------
planechase_crash:
//----------------------------------------------------------------------
	$yak thread yak_pullup
	wait 3
	$stuka thread stuka_crash

end

//----------------------------------------------------------------------
yak_pullup:
//----------------------------------------------------------------------
$yak show
$yak followpath $yak_path 1500 2000 384
self playsound plane
self waitmove
$yak hide
end

//----------------------------------------------------------------------
stuka_crash:
//----------------------------------------------------------------------

$stuka show
$stuka anim smoke
$stuka followpath $stuka_path 1500 2000 384
self playsound plane
self waitmove
$stuka hide
thread global/exploder.scr::explode 37
end

//----------------------------------------------------------------------
health_pickup_1:
//
//	Drive T34 into Barrel trigger to gain heath based on difficulty
//----------------------------------------------------------------------

	dprintln "(before)T34 HEALTH=" $playertank.health
	dprintln "Level.skill=" level.skill

	local.skill = level.skill

	switch ( local.skill )
	{
		case	0:
		dprintln "setting skill level 0 to 5000"
		$playertank.health = 5000
		break;
		case	1:
		dprintln "setting skill level 1 to 3000"
		$playertank.health = 3000
		break;
		default:
		dprintln "setting skill level 2 to 1000"
		$playertank.health = 1000
		break;
	}

	$player playsound tank_health
	$health_pickup_1 remove
	dprintln "(after)T34 HEALTH=" $playertank.health

end

//----------------------------------------------------------------------
health_pickup_2:
//
//	Drive T34 into Barrel trigger to gain heath based on difficulty
//----------------------------------------------------------------------

	dprintln "(before)T34 HEALTH=" $playertank.health
	dprintln "Level.skill=" level.skill

	local.skill = level.skill

	switch ( local.skill )
	{
		case	0:
		dprintln "setting skill level 0 to 5000"
		$playertank.health = 5000
		break;
		case	1:
		dprintln "setting skill level 1 to 3000"
		$playertank.health = 3000
		break;
		default:
		dprintln "setting skill level 2 to 1000"
		$playertank.health = 1000
		break;
	}

	$player playsound tank_health
	$health_pickup_2 remove
	dprintln "(after)T34 HEALTH=" $playertank.health

end

//----------------------------------------------------------------------
health_pickup_3:
//
//	Drive T34 into Barrel trigger to gain heath based on difficulty
//----------------------------------------------------------------------

	dprintln "(before)T34 HEALTH=" $playertank.health
	dprintln "Level.skill=" level.skill

	local.skill = level.skill

	switch ( local.skill )
	{
		case	0:
		dprintln "setting skill level 0 to 5000"
		$playertank.health = 5000
		break;
		case	1:
		dprintln "setting skill level 1 to 3000"
		$playertank.health = 3000
		break;
		default:
		dprintln "setting skill level 2 to 1000"
		$playertank.health = 1000
		break;
	}

	$player playsound tank_health
	$health_pickup_3 remove
	dprintln "(after)T34 HEALTH=" $playertank.health

end

//----------------------------------------------------------------------
health_pickup_4:
//
//	Drive T34 into Barrel trigger to gain heath based on difficulty
//----------------------------------------------------------------------

	dprintln "(before)T34 HEALTH=" $playertank.health
	$playertank.health = 5000
	$health_pickup_4 remove
	dprintln "(after)T34 HEALTH=" $playertank.health
	$player playsound tank_health

end

//----------------------------------------------------------------------
removehealth:
//
//	Drive T34 into Barrel trigger to gain heath based on difficulty
//----------------------------------------------------------------------

	dprintln "Level.skill=" level.skill

	local.skill = level.skill

	switch ( local.skill )
	{
		case	0:
		dprintln "removing final health"
		$finalheathtrigger remove
		$health_pickup_4 remove
		break;
		case	1:
		dprintln "removing final health"
		$finalheathtrigger remove
		$health_pickup_4 remove
		break;
	}

	dprintln "(after)T34 HEALTH=" $playertank.health

end
