//-------------------------------------------------------------------
main:
//
//
//-------------------------------------------------------------------

	level.skip_briefing = 0
	level.bTrainReady	= 0
	level.mg42_gag = 0

	$player takeall


	self waitthread Init

	self waitthread Briefing

	level.cappy targetname cappy

	level.bJumpOverWall	= 0

	$train hide
	$train notsolid

//	wait 10
//	self thread GotoBarn
//	wait 10
//	thread DoTrain
//	level.cappy nodamage
//	level.friendly2 nodamage
//	level.friendly3 nodamage
//	wait 1
//	level.friendly3 damage NULL 99000 NULL (0 0 0) (0 0 0) (0 0 0) 0 9 0 0

// make sure friendlies don't go after these guys until we're ready...
	$Patrol1 thread IgnoreEnemy
	$Patrol1b thread IgnoreEnemy
	$Patrol2 thread IgnoreEnemy

	level.cappy.destination = $player
	level.friendly2.destination = level.friendly3
	level.friendly3.destination = level.cappy

	level.cappy.distance = 175
	level.friendly3.distance = 225
	level.friendly2.distance = 275	// friendly2 is last..

	// don't want this because it causes the guys to get inside each other
	level.friendly_auto_arrange	= 0

	// turn the smoke off on the train until we're ready..
	$train anim smoke_off


end

//-------------------------------------------------------------------
IgnoreEnemy:
//
//	have friendlies ignore
//-------------------------------------------------------------------
	self threatbias ignoreme
	self thread OnEnemyIgnoreStop
	end

//-------------------------------------------------------------------
OnEnemyIgnoreStop:
//-------------------------------------------------------------------
	self waittill hasenemy
	self threatbias 5000

	end
//-------------------------------------------------------------------
Init:
//-------------------------------------------------------------------
	self thread DeathCheck
	self.bDisableGermans = 1

//	self thread MoveDoneRadiusCheck
	level.bWaitingForPlayer = 0

//	self thread gags/t1l2_friend.scr::FriendlyCheck

	// become friend 
	self waitthread FriendStart

	// jsl--> set this to 1 when dialog animations are in!!
	self.animated		= 0

	// turn off AI for flak germans until we're ready..
	$FlakGerman1 exec global/disable_ai.scr
	$FlakGerman2 exec global/disable_ai.scr
	$FlakGerman3 exec global/disable_ai.scr

	$FlakGerman1 nodamage
	$FlakGerman2 nodamage
	$FlakGerman3 nodamage


	End


//-------------------------------------------------------------------
Briefing:
//
//	Do the briefing shiznit...
//	
//-------------------------------------------------------------------

	// jsl-->for some reason, without this wait, the solider anims below won't play???!!
	wait 0.1

 	self waitthread FriendStop

//// testtest

	if ( level.skip_briefing==1 )
	{
		fadein 0.1 0 0 0 1
		goto skip
	}

	self.no_idle = 1


	$player stufftext "tmstart sound/music/Emmerich.mp3"


	// lock player down here...

	$player physics_off

	drawhud 0

	self thread global/LoopAnim.Scr::LoopAnim 12C101_Idle

	self turnto $player
	self lookat $player

	fadein 2.5 0 0 0 1

	level.friendly2 anim 12A005_soldier1
	level.friendly3 anim 12A006_soldier2

	wait 2

	// "Motherbird to groundhog, come in groundhog." maps "t "	
	self Playsound dfr_T1L2_BR6801 wait
	self waittill sounddone 
	wait 0.4

//	goto testskip

	self waitthread global/LoopAnim.Scr::LoopAnimStop

	// "Groundhog receiving, go ahead motherbird." maps "t "

	self waitthread say_to dfr_T1L2_BC6903 12C103_Dialogue01 NULL 1

	self thread global/LoopAnim.Scr::LoopAnim 12C102_RadioIdle

	wait 1

	// "Okay groundhog, listen up.  The Germans are scrambling to reinforce their artillery divisions north of you." maps "t "
	self playsound dfr_T1L2_BR6802 wait
	self waittill sounddone

	wait 0.1

	// "There's only one bridge in your area they can be using and it's held by a Colonel Hildebrandt." maps "t "
	self playsound dfr_T1L2_BR6803 wait
	self waittill sounddone

	wait 0.1

	// "Your mission is to eliminate the colonel and destroy that bridge." maps "t "
	self playsound dfr_T1L2_BR6804 wait
	self waittill sounddone

	// "Reports indicate the bridge is heavily defended, but your orders are to proceed and succeed at all costs.  Over." maps "t "
	self playsound dfr_T1L2_BR6805 wait
	self waittill sounddone

	wait 0.2
testskip:

	// "Copy that motherbird, over and out." maps "t "
	self waitthread global/LoopAnim.Scr::LoopAnimStop

	self waitthread say_to dfr_T1L2_BC6904 12C104_Dialogue02 NULL 1

	self.no_idle = 0

	// "Sergeant, you're on point. Let's move." maps "t "
	self anim 12C105_Dialogue03
	self waittill animdone
//	self waitthread say_to dfr_T1L2_BC6905 12C105_Dialogue03 $player 0
//	self waitthread global/LoopAnim.Scr::LoopAnimStop

	$player physics_on
	drawhud 1



	// save game here so they don't have to keep hearing the briefing....
	exec global/autosave.scr 1

skip:
//	fadein 0.1 0 0 0 1

	self waitthread FriendStart

	// don't want cappy picking up health..
	wait 1
	self.health_pickup = FALSE

	// give him his weapons here..
	$player item weapons/Webley_Revolver.tik 
	$player item weapons/enfield.tik 
	$player item weapons/sten.tik 
	$player item weapons/mills_grenade.tik   
	$player item weapons/M18_smoke_grenade.tik  

	$player ammo pistol 32
	$player ammo smg 128
	$player ammo rifle 50
	$player ammo smokegrenade 3
	$player ammo grenade 3

	$player useweaponclass smg

	$player item items/binoculars.tik
	waitthread global/items.scr::add_item "binoculars" noprint	// Show binoculars inventory icon
	waitthread global/items.scr::add_item "explosive"	noprint // Show explosive inventory icon



	End

//-------------------------------------------------------------------
DeleteGrenade:
//-------------------------------------------------------------------

	wait 2.7
	$projectile[$projectile.size] delete

	end

//-------------------------------------------------------------------
GotoBarn:
//	
//	There are a couple of path points on the way to the barn.
//		"BarnDoor"		- Run to the door and kick it in...
//		"BarnInside"	- Run inside to here before fighting.
//		"Grendade1"		- GrenadeHint object to toss the grenade at
//-------------------------------------------------------------------

	// see if we need some health...
	level.friendly2 thread BeHealthy
	level.friendly3 thread BeHealthy
	self waitthread BeHealthy

	// "Good work chaps, now let's destroy this cannon." maps "t1l2  "
	self say dfr_T1L2_BC6962
	wait 2.5

	// "Here we go.  Barnes, you're with me.  Wait for my order." maps "t "
	self waitthread say_to dfr_T1L2_BC6906 12C106_Dialogue04 $player

	self.movedoneradius		= 6
	
	level.friendly2 thread gags/t1l2_Friend.scr::RunForGrenadeCover $grenade_run1
	level.friendly3 thread gags/t1l2_Friend.scr::RunForGrenadeCover $grenade_run2

	self waitthread CappyRunTo $BarnDoor

////////////////	self origin $BarnDoor.origin

	self turnto $Grenade1
	self lookat $Grenade1


//  make sure my friendly grunt's don't get in the way..

	// pause our friendlies

	$Flak88A thread gags/T1L2_Flak88.Scr::StopFlak88Loop

	// make all the friendlies invulnerable here..

	level.cappy nodamage
	level.friendly2 nodamage
	level.friendly3 nodamage

	self exec global/disable_ai.scr
	$Grenade1.origin -= ( 0 0 10 )

	// cheezy way to make sure grenade never "misses"
	thread DeleteGrenade
	
	self waitthread ThrowGrenade $Grenade1 800 0

	wait 3.2
	$barndoor_lockedtrigger remove
	self waitthread global/exploder.scr::explode 1

	local.o = $BarnDoor.origin - ( 150 0 0 )

	radiusdamage local.o 180 150 0

	level.cappy takedamage
	level.friendly2 takedamage
	level.friendly3 takedamage

	// turn on flak guys...
	$FlakGerman1 exec global/enable_ai.scr
	$FlakGerman2 exec global/enable_ai.scr
	$FlakGerman3 exec global/enable_ai.scr

	$FlakGerman1 takedamage
	$FlakGerman2 takedamage
	$FlakGerman3 takedamage


	if ( level.friendly3dead==0 )
		level.friendly3 thread gags/t1l2_friend.scr::RemindPlayer
	else
		level.friendly2 thread gags/t1l2_friend.scr::RemindPlayer

	self turnto NULL
	self lookat NULL

	// "Let's go!" maps "t "
	self say 12C108_Dialogue05	// lip only
//	self waitthread say_to dfr_T1L2_BC6907 12C108_Dialogue05

	//////////////////////	self waitthread global/say_to.scr::main dfr_T1L2_BC6907 NULL 999 0 0


	// turn off ai until we get in there...
	self exec global/disable_ai.scr
	self runto $BarnInside
	self waittill movedone

	// be a friend to the player again...
///////////////	self waitthread FriendStart

	// ai takes over here.
	// when the germans are dead, BlowFlak88 will get called...
	self exec global/enable_ai.scr


	// allow these guys to fight if needed...
	level.friendly2 exec global/enable_ai.scr
	level.friendly3 exec global/enable_ai.scr


	End

//-------------------------------------------------------------------
BlowFlak88:
//
//	Instruct player to place the charge and then run the hell out of there...
//-------------------------------------------------------------------

	self waitthread FriendStop

	// "Sergeant, get a demo charge on this Flak cannon!  Set the charge and meet up with the rest of the squad outside." maps "t "
	self waitthread say_to dfr_T1L2_BC6908 12C110_Dialogue06 $player

	$Flak1ThrobbingBox show

// tell my grunts to go take cover...

	level.friendly2 thread gags/t1l2_friend.scr::GoTakeFlak88Cover2
	level.friendly3 thread gags/t1l2_friend.scr::GoTakeFlak88Cover3

// 	Cappy take cover...
	self.movedoneradius = 6
	self waitthread CappyRunTo $Flak88Cover1

	self turnto $BarnDoor

	wait 0.75

	self turnto NULL

//	level.friendly2 thread gags/t1l2_friend.scr::FollowCappy
//	level.friendly3 thread gags/t1l2_friend.scr::FollowCappy


	End

//-------------------------------------------------------------------
SpawnInvestigators:
//	
//	Spawns the squad of investigators who want to know who blew up
//	their FLAKK 88!
//
//	spawn them and send them to the barn.
//
//-------------------------------------------------------------------

	wait 0.4

//	self turnto NULL

//	level.friendly2 turnto NULL
//	level.friendly3 turnto NULL

	self waitthread global/ai.scr::spawnset 122 "barn_investigators"

	self waitthread WaitTillCount $barn_investigators 5

	// there are only 2 left, so become friendly to the player again...

	self waitthread FriendStart

	if ( level.friendly2dead==0 )
		level.friendly2 thread gags/t1l2_friend.scr::FriendlyStart

	if ( level.friendly3dead==0 )
		level.friendly3 thread gags/t1l2_friend.scr::FriendlyStart

	self waitthread WaitTillCount $barn_investigators 0

	// they're all dead, so become healthy here if needed...

	level.friendly2 thread BeHealthy
	level.friendly3 thread BeHealthy
	self waitthread BeHealthy

	self waitthread FriendStop

	self waitthread CappyRunTo $player 250

	// if we're too close to the train, then don't do this...
	local.temp = vector_length($player.origin - $grate_demonode.origin)

	if ( local.temp > 1500 )
	{
		// "Keep moving.  Staggered formation, go!" maps "t "
		self say 12C112_Dialogue08
//		self waitthread say_to dfr_T1L2_BC6910 12C112_Dialogue08 $player
	}

	level.bTrainReady = 1

	self thread FriendStart

	End

//------------------------------------------------------------------
SpawnOutsideBarn:
//
//	These are the initial baddies the player goes after before the
//	barn incident...
//
//------------------------------------------------------------------

//testtest
//	dprintln "SpawnOutsideBarn!"
//	self thread GotoBarn
//	End

	self waitthread global/ai.scr::spawnset 100 "barn_outside_guys"
	self thread WaitForBarnClear

	End

//------------------------------------------------------------------
WaitTillCount local.name local.count:
//------------------------------------------------------------------

	local.alive	= 99

	while ( local.name!=NIL && local.name!=NULL && local.alive>local.count )
	{
		wait 1
		local.alive = 0
		for (local.i=1;local.i<=local.name.size;local.i++)
		{
			if ( local.name[local.i].health > 0 )
			{
				local.alive++
				local.sName	= local.name[local.i].targetname
			}
		}

		if ( local.alive>0 )
			dprintln "There are still " local.alive " of " local.sName " out there!"
	}


	End


//------------------------------------------------------------------
WaitForBarnClear:
//------------------------------------------------------------------

	dprintln "Waiting for barn clear.."

	self waitthread WaitTillCount $barn_outside_guys 0

	self waitthread FriendStop
	self thread GotoBarn

	End



//-------------------------------------------------------------------
ThrowGrenade local.hint local.velocity local.toss:
//
//	Toss a grenade into the barn....
//	
//		p1 - grenade_hint name
//		p2 - velocity
//		p3 - pass 1 to do underhand toss or 0 to throw.
//-------------------------------------------------------------------

	self type_idle idle
	self ammo_grenade 10

	local.grenadescript = self calcgrenadetoss2 local.hint local.velocity

	if (local.grenadescript != "")
	{
		self turnto local.hint

		wait 0.2

// 		self exec anim/say_grenade_thrown.scr

		wait 0.2

		self.blendtime = 0.3
		self playsound m2frag_snd_grenade_pull
		self setmotionanim grenade_pullout 
		self waittill flaggedanimdone
		waitframe

		self.blendtime = 0.1	// This animation should blend perfectly with the previous one
		self playsound m2frag_snd_grenade_throw
		if ( local.toss==1 )
		{
			self setmotionanim grenade_toss
		}
		else
		{
			self setmotionanim grenade_throw
		}
			
		self waittill flaggedanimdone
		waitframe
		self.blendtime = 0.1	// This animation should blend perfectly with the previous one
		self setmotionanim grenade_weapon_rearm
		self waittill flaggedanimdone

//		wait 3
	}
	else
	{
		dprintln "failed grenade test...
	}

End

//-------------------------------------------------------------------
FriendStart:
//
//	Cappy can become the friend of the player and follow him around
//	by calling this thread...
//-------------------------------------------------------------------

	self.movedoneradius		= 275
	self.waittime			= 0.5	// keep cappy close to player...
	self.distance			= 275
//	self.mins				= 60
//	self.maxs				= 100
//	self.destination		= $player
	self.friendtype			= 1
	self thread gags/t1l2_friend.scr::FriendlyStart
	self.friendtype = 1

End

//-------------------------------------------------------------------
FriendStop:
//
//	Call this when you want the captain to stop following the player
//  so you can have him do something useful...
//-------------------------------------------------------------------
//	self.distance			= 6
//	self.movedoneradius		= 6

	self waitthread gags/T1L2_friend.scr::FriendlyStop

End


//-------------------------------------------------------------------
DeathCheck:
//
//
//-------------------------------------------------------------------
	
	self waittill death

	iprintlnbold "The Captain has been killed in action."
	iprintlnbold "You failed the mission."
//	fadeout 3 0 0 0 1
	wait 3
	missionfailed

	End

//------------------------------------------------------------------
SpawnHouseAI:
//
//	Spawn them with a bit of a delay...
//------------------------------------------------------------------

	wait 2
	level.disableAI	= 1
	thread global/ai.scr::spawngroup 200 2
	wait 0.5
	thread global/ai.scr::spawngroup 200 3
	wait 0.5
	thread global/ai.scr::spawngroup 200 4
	wait 0.5
	thread global/ai.scr::spawngroup 200 5
	wait 0.5
	thread global/ai.scr::spawngroup 200 6
	wait 6

	level.disableAI = 0

	End

//-------------------------------------------------------------------
TellPlayerFlushout:
//-------------------------------------------------------------------
	
	wait 1
	
	// "Cover!  Barnes, get to the back of the house!  We'll flush them, and you take them out.  Go now!" maps "t "
	self say dfr_T1L2_BC6909

	End


FakeGrenadeThrow:
	wait 3.25

	$fake_grenade projectile models/projectiles/mills_grenade.tik
	$fake_grenade mindelay 0
	$fake_grenade maxdelay 0
	$fake_grenade minduration 0.01	// make sure it shoots it right away
	$fake_grenade maxduration 0.01  // make sure it shoots it right away
	$fake_grenade turnon
end

//-------------------------------------------------------------------
DoFlushOut:
//
//	We're under the bridge, do the flushout gag....
//-------------------------------------------------------------------

 	self waitthread FriendStop

	self exec global/disable_ai.scr

	self thread TellPlayerFlushout

	// tell our buddy to shoot at the house as well...

	if ( level.friendly2dead==0 && level.friendly3dead==0 )
	{
		level.friendly2 thread gags/t1l2_friend.scr::AttackHouse
	}

/////////////////	self waitthread CappyRunTo $ThrowGrenade2
	// run to just before the window.
	self.movedoneradius = 6
	self waitthread CappyRunTo $ThrowGrenade2Crouch
	
	// crouch from here...

//	self exec global/moveto.scr anim/crouch_run.scr $ThrowGrenade2
//	self waittill movedone

	// time to get them coming out of the house...
	self thread SpawnHouseAI

//	self exec global/stand.scr
//	self waittill animdone
//	self turnto $Grenade2Hint
//	self lookat $Grenade2Hint

	thread FakeGrenadeThrow

	self anim 12C109_GrenadeWindow
	self waittill animdone

	self turnto NULL
	self lookat NULL

	// have our grenade toss...
	// $fake_grenade turnon

//	self waitthread ThrowGrenade $Grenade2Hint 600 0

	self aimat $HouseTarget1

	local.xx = 0

	for (local.xx = 0; local.xx < 20; local.xx++ )
	{
		self fire
		wait 0.15
	}

	wait 1.5

//	self anim reload
//	self waittill animdone

//	self aimat $HouseTarget1

	for (local.xx = 0; local.xx < 20; local.xx++ )
	{
		self fire
		wait 0.15
	}

	self exec global/enable_ai.scr
 	self waitthread FriendStart

	// say our down to 3 dialog if wilson just got killed in our mg42 gag...
	if ( level.mg42_gag==1 )
		self thread DownToThree


	End

//-------------------------------------------------------------------
WaitForPlayer:
//-------------------------------------------------------------------

	while ( vector_length($player.origin - self.origin) < 80 )
	{
		wait 1
	}

	End
		

//-------------------------------------------------------------------
DoTrain:
//-------------------------------------------------------------------

	dprintln "Doing TRAIN!!!!!"

		/*
	// put guys on back...
	local.guy = spawn human/german_wehrmact_soldier
	local.guy physics_off
	local.guy attach $train_caboose "passenger3" 0 (0 0 0)

	local.guy = spawn human/german_wehrmact_soldier
	local.guy physics_off
	local.guy attach $train_caboose "passenger4" 0 (0 0 0)
	*/

	local.origin = $train.origin + (0 0 64)
	local.soundent = spawn script_origin origin local.origin
	local.soundent notsolid
	local.soundent thread DoTrainSound

	$train show
	$train anim smoke_on
	$train waittill animdone

	thread hidetrain
	$train moveanim 12P004_train
	$train waittill animdone

	// MJG: this elegantly shuts off the corona that's created elsewhere
	$train.corona = 0
	waitframe
	waitframe

//	local.soundent delete
	$train delete

	End

//-------------------------------------------------------------------
hidetrain:
// have to hide the train after certain amount of time because
// for some reason, it appears behind the player!!! (before anim finishes!)
//-------------------------------------------------------------------
	wait 12
	// give it a few more seconds to play the rest of its sound
	$train hide
	$train notsolid
	end

//-------------------------------------------------------------------
DoTrainSound:
//-------------------------------------------------------------------

	self moveWest 11000
	self time 8.8
	self move
	self show
	dprintln "Playing train sound!"
	self svflags "+broadcast"
	// jsl-->Use loopsound to have the sound travel with the object.
	self loopsound train_by
	// delete it 12 seconds later so it doesn't loop...
	wait 12

	self delete

	End
//-------------------------------------------------------------------
TrainDialog:
//-------------------------------------------------------------------

	thread DoTrain

//	wait 1

	self waitthread FriendStop
	self exec global/disable_ai.scr
	
	///// self waitthread global/say_to.scr::main dfr_T1L2_BC6911 $player	200 0 0
	// "They're moving artillery along those tracks.  We've got to blow the bridge to cut off their supply lines.  Move out!" maps "t "

	self.movedoneradius = 275
	self.distance = 275

	self runto $player
	self waittill movedone

//	self.movedoneradius = 6

	self lookat $demotrain
	self turnto $demotrain
	wait 0.8

	self waitthread say_to dfr_T1L2_BC6911 12C113_Dialogue09 NULL

	if ( level.friendly2!=NIL && isAlive level.friendly2 )
		level.friendly2 thread gags/t1l2_friend.scr::FriendlyStop

	if ( level.friendly3!=NIL && isAlive level.friendly3 )
		level.friendly3 thread gags/t1l2_friend.scr::FriendlyStop

	self.movedoneradius = 6
	self waitthread CappyRunTo $cappytrain

	// tell friendlies what to do
	if ( level.friendly3dead==0 )
	{
		level.friendly3 thread gags/t1l2_friend.scr::BlowUpGate
	}
	else
	{
		level.friendly2 thread gags/t1l2_friend.scr::BlowUpGate
	}

	// "Galloway, demo this drainage grate!  Barnes, take cover over here with me! Move!" maps "t "
	self waitthread say_to dfr_T1L2_BC6956 12C119_Capdialog14 $player 1

	self lookat $demotrain
	self turnto $demotrain

	if ( level.friendly2dead==0 && level.friendly3dead==0 )
		level.friendly2 thread gags/t1l2_friend.scr::DoGateCover

	wait 0.5

	// crouch...
	self exec global/crouch.scr


	self lookat NULL
	self turnto NULL

	End

//-------------------------------------------------------------------
DestroyFlak2:
//
//	Have captain open the door...
//	
//-------------------------------------------------------------------

	level.longtermfriend thread BeHealthy
	self waitthread BeHealthy

	self exec global/disable_ai.scr

	level.longtermfriend exec global/disable_ai.scr

	// don't want to be a friend anymore...
	self waitthread FriendStop
	level.longtermfriend thread gags/t1l2_friend.scr::FollowCappy

	// "Let's clear this house and see if we can't get a better vantage point of the bridge.  Follow me." maps "t "
//	self waitthread global/say_to.scr::main dfr_T1L2_BC6912 $player	200 0 0
	self waitthread say_to dfr_T1L2_BC6912 12C114_Dialogue10 $player

	$biggate_lockedtrigger delete
	///testtest self waitthread BeHealthy


////////	$compoundgate open self
	
	level.longtermfriend exec global/enable_ai.scr
	self exec global/enable_ai.scr

	level.longtermfriend thread gags/t1l2_friend.scr::GetNervous

	// run to these spots.  Cappy's ai will have him stop whenever he sees
	// the enemy.  He'll still continue to go to these destinations.
	// hopefully by the time he gets to the final destination, everyone will
	// be cleared out...

	self waitthread CappyRunTo $CappyAttack1
	self waitthread CappyRunTo $CappyAttack2

//	level.friendly2 thread BeHealthy
//	self thread BeHealthy

	self.movedoneradius = 6
	// tell our buddy to go to his spot...
	level.longtermfriend thread gags/t1l2_friend.scr::GoWaitForJump

	self waitthread CappyRunTo $CappyPlayerWait

	// make sure all the upstairs nazi's are gone...
	self waitthread WaitTillCount $upstairs_guys 0

	// make sure we're done attacking
	wait 0.4
	while (self.thinkstate == "attack")
	{
		// make sure we're done attacking!!!!!!
		wait 2
	}

	self exec global/disable_ai.scr

	self.movedoneradius = 6
	self waitthread CappyRunTo $CappyPlayerWait

	// tell our buddy to go to his spot...
	level.longtermfriend thread gags/t1l2_friend.scr::GoWaitForJump2

	self waitthread WaitForPlayer

	// Now, wait for the player to trigger us from here...
	self thread PlayerUpstairs

	End

//-------------------------------------------------------------------
PlayerUpstairs:
//
//		Tell him to take a look
//-------------------------------------------------------------------

	level.nWaitingForPlayer = 0

	self.movedoneradius = 6
	self waitthread CappyRunTo $CappyPlayerWait

	self lookat $player
	self turnto $player

	self waitthread WaitForPlayer

	$FlakGerman4 takedamage
	$FlakGerman5 takedamage
	$FlakGerman6 takedamage

	// have our buddy feel better...
	level.longtermfriend thread BeHealthy

	wait 0.3
	// "Take a look Sergeant." maps "t "
	self waitthread say_to dfr_T1L2_BC6915 12C115_Capdialog11 $player 1

	self lookat NULL
	self turnto NULL

	level.nWaitingForPlayer = 2

	self thread global/LoopAnim.Scr::LoopAnim 12C118_Capidle

	while (level.nWaitingForPlayer==2)
	{
		wait 0.5
	}

	self waitthread global/LoopAnim.Scr::LoopAnimStop

	level.bJumpOverWall	= 1

	// "Private Wilson will go scout the bridge.  You take out the eighty-eight down there." maps "t "
	// "I'll provide suppressing fire from here.  Wait for my signal before attacking.  Move out!" maps "t "

	self waitthread say_to dfr_T1L2_BC6914 12C116_capdialog12 $player 1

	self waitthread BeHealthy

	self lookat NULL
	self turnto NULL

	level.nWaitingForPlayer = 3	// PlayerOutside

////	self waitthread BeHealthy
	
	End

//-------------------------------------------------------------------
PlayerAtWindow:
//
//-------------------------------------------------------------------

	level.nWaitingForPlayer = 0

	End

//-------------------------------------------------------------------
PlayerOutside:
//
//		OK, let's do our suppressing fire..
//
//-------------------------------------------------------------------
	level.nWaitingForPlayer = 4

	// stop the flak guys!


// let's make sure they notice the captain....
	$FlakGerman4 hearing 5000
	$FlakGerman5 hearing 5000
	$FlakGerman6 hearing 5000

	$FlakGerman4 sight 5000
	$FlakGerman5 sight 5000
	$FlakGerman6 sight 5000

	thread TurnUpTheFog

	self.movedoneradius = 6
	self waitthread CappyRunTo $CaptainCoverFire
//	self origin $CaptainCoverFire.origin

	
	level.longtermfriend origin $FriendTeleportBoat2.origin
//	level.friendly2 hide

	self aimat $Flak2Hint

	self thread GetGermansAttention

	local.xx = 0
	while ( level.nWaitingForPlayer==4 )
	{
		for (local.yy = 0; local.yy < 4; local.yy++ )
		{
			for (local.xx = 0; local.xx < 20; local.xx++ )
			{
				self fire
				wait 0.15
				if ( level.nWaitingForPlayer!=4 )
					break
			}
			if ( level.nWaitingForPlayer!=4 )
				break
		}

		if ( level.nWaitingForPlayer==4 )
			wait 2
	}

	self aimat NULL

	// now, let's run to our waiting place...
	self runto $CappyCheat


	End

//-------------------------------------------------------------------
GetGermansAttention:
//	stop the flak88 guys and have them run to the captain..
//-------------------------------------------------------------------

	// don't have "pain" cancel the gag anymore...
	waitthread gags/T1L2_Flak88.Scr::StopFlak88Guys

	// they'll never get there.. Once they see either the player or cappy, they'll start firing...
	$FlakGerman4 runto level.friendly1
	$FlakGerman5 runto level.friendly1
	$FlakGerman6 runto level.friendly1


	End

//-------------------------------------------------------------------
CoverFireStop:
//
//	Just set level.nWaitingForPlayer to 0 and we'll stop...
//-------------------------------------------------------------------
	level.nWaitingForPlayer = 0
	End


//-------------------------------------------------------------------
Flak88_2_GonnaGoBoom:
//-------------------------------------------------------------------
	// teleport to where we're supposed to be...
	//self runto $CappyCheat.origin

	
	End

//-------------------------------------------------------------------
Flak88_2_BlownUp:
//-------------------------------------------------------------------

	// "Nice work, Sergeant." maps "t "
	self exec global/enable_ai.scr
	self waitthread CappyRunTo $player 250
//	self waitthread say_to dfr_T1L2_BC6916 12C117_Capdialog13 $player
	self say 12C117_Capdialog13

	level.longtermfriend thread gags/t1l2_friend.scr::FinishLevel

	self thread FriendStart

	End

//-------------------------------------------------------------------
GoToBoat:
//
//	Just run to the boat....
//-------------------------------------------------------------------

	self turnto NULL
	self lookat NULL

	self waitthread FriendStop
	self exec global/disable_ai.scr

	self.movedoneradius = 6
	self waitthread CappyRunTo $TheBoat2

	End

//-------------------------------------------------------------------
BeHealthy:
//
//		Drink from our own little canteen if needed...
//
//-------------------------------------------------------------------

	while (self.thinkstate == "attack")
		waitframe

	if (self.health >= self.maxhealth)
	{
		end
	}

	self exec global/disable_ai.scr
	self nodamage

//	self.friendtype = -1
	self holster
	wait 0.4
	self lookat NULL
	self upperanim pass_canteen_drink
	self waittill upperanimdone
	self takedamage
	self.health = self.maxhealth
//	self.friendtype = 1
	self unholster

	self exec global/enable_ai.scr


	End

// ---------------------------------------------------------------
DownToThree:
//
//	say our down to three dialog when we're done fighting...
// ---------------------------------------------------------------

	dprintln "cappy preparing to play down to three dialog!"
	wait 1

	while (self.thinkstate == "attack" )
		wait 0.3

	dprintln "cappy playing down to three dialog!"
	self say dfr_T1L2_BC6963

	end

//-------------------------------------------------------------------
TurnUpTheFog:
//-------------------------------------------------------------------

	for (local.i=5500;local.i>=3500;local.i-=4)
	{
		$world farplane local.i
		wait 0.01
	}

	end


//-------------------------------------------------------------------
say_to local.sound local.anim local.ent local.movecloser:
//-------------------------------------------------------------------

	if ( local.movecloser==NIL )
		local.movecloser	= 0


	self.movedoneradius		= 275
	self.distance			= 275

//	// don't need this anymore...
	local.sound = NULL

	self waitthread global/dialog_to.scr::dialog local.sound local.anim local.ent 250 local.movecloser

	End

//-------------------------------------------------------------------
CappyRunTo local.ent local.movedoneradius:
//
//	Makes sure we actually get there...
//-------------------------------------------------------------------

	if ( local.ent==NIL )
	{
		dprintln "CappyRunTo: entity is NULL: " local.ent
		End
	}

	if ( local.movedoneradius!=NIL )
		self.movedoneradius = local.movedoneradius

CappyRunTo_DoAgain:
	self runto local.ent
	self waittill movedone

	if ( parm.movedone != 1 )
		goto CappyRunTo_DoAgain

	self.movedoneradius		= 275

	End


//-------------------------------------------------------------------
MoveDoneRadiusCheck:
//-------------------------------------------------------------------
	dprintln self.targetname " move done radius=" self.movedoneradius
	wait 1.5
	goto MoveDoneRadiusCheck
