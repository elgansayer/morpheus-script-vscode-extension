//-------------------------------------------------------------------
//	t1L2_friend.Scr
//	Jeff Leggett
//	06/13/2002
//
//	Script used by the two friendlies that follow the captain around
//
//
//
//		trigger setthreads:
//
//			gags\t1l2_friend.scr::PlayerInBarn
//
//-------------------------------------------------------------------

//-------------------------------------------------------------------
main:
//
//
//-------------------------------------------------------------------
	thread Friendly2DeathCheck
	thread Friendly3DeathCheck

	level.friendly2dead = 0
	level.friendly3dead = 0
	level.remind_player = 1

	level.longtermfriend = level.friendly2

end

//-------------------------------------------------------------------
Init:
//	Initialize the friendly...
//	
//-------------------------------------------------------------------

	self.waittime				= 1
//	self.distance				= 100
//	self.mins					= 60
//	self.maxs					= 100
//	self.destination			= level.friendly1	
//	self.destination			= $player			// the captain...
	self.friendtype				= 1
	self thread FriendlyStart
	self.friendtype				= 1

	$upstairs_door lock

//	thread FriendlyCheck


	End

//-------------------------------------------------------------------
GoTakeFlak88Cover2:
//
//
//-------------------------------------------------------------------

	self waitthread FriendlyStop
	self exec global/disable_ai.scr

	self waitthread FriendlyRunTo $Flak88Cover2

	// face the right way
	self turnto $BarnDoor

	wait 0.5

	self turnto NULL

	self exec global/enable_ai.scr

	End
	
//-------------------------------------------------------------------
GoTakeFlak88Cover3:
//
//
//-------------------------------------------------------------------
	self waitthread FriendlyStop
	self exec global/disable_ai.scr

	self waitthread FriendlyRunTo $Flak88Cover3

	// face the right way
	self turnto $BarnDoor

	wait 2

	self turnto NULL

	self exec global/enable_ai.scr

	End
	

//-------------------------------------------------------------------
DoMachineGunAttack local.bTargetMe:
//-------------------------------------------------------------------

	if ( local.bTargetMe==1 )
		$machinegun target self

	$machinegun accuracy 0
	$machinegun turnon
	level.cappy thread gags/t1l2_cappy.scr::DoFlushOut

	End

//-------------------------------------------------------------------
GetKilled:
//
//	Time to die... Walk to the death spot
//-------------------------------------------------------------------

	self lookat NULL
	
	self.movedoneradius = 6
	self waitthread FriendlyStop

	if ( level.friendly3dead==0 && level.friendly2dead==0 )
	{
		// both guys are alive, so it's okay to die.
		level.mg42_gag = 1

		self takedamage	
		self waitthread FriendlyRunTo $MG42Window

		self turnto $machinegun
		self lookat $machinegun


		// just take it in the chest like a man!
		wait 0.23

		self turnto NULL
		self lookat NULL
	}
	else
	{
		// only 1 guy is alive, so don't die here...
//		dprintln "ONLY ONE GUY LEFT, SO I'LL RUNTO THE AVOID AREA.."
		self waitthread FriendlyRunTo $MG42Window_Avoid
		self exec global/enable_ai.scr
		wait 1
		self thread FriendlyStart
	}
	
	End

//-------------------------------------------------------------------
AttackHouse:
//
//		Shoot at the house...
//-------------------------------------------------------------------


	self waitthread FriendlyStop

	self exec global/disable_ai.scr

	self waitthread FriendlyRunTo $BuddyAttackHouse

	if ( isAlive $WindowGerman )
	{
		$WindowGerman exec global/disable_ai.scr
		$WindowGerman runto $GermanWindow
		$WindowGerman waittill movedone
		$WindowGerman turnto self
		wait 0.3
		$WindowGerman exec global/enable_ai.scr
	}
	else
	{
		wait 1.5
	}

	self aimat $HouseTarget2

	for (local.xx = 0; local.xx < 8; local.xx++ )
	{
		self fire
		wait 0.5
	}

	wait 1.5

//	self anim reload
//	self waittill animdone

//	self aimat $HouseTarget2

	for (local.xx = 0; local.xx < 8; local.xx++ )
	{
		self fire
		wait 0.5
	}

	self exec global/enable_ai.scr
		
	self waitthread FriendlyStart

	End

//-------------------------------------------------------------------
FollowPlayer:
//-------------------------------------------------------------------
//	self.destination			= $player

	End

//-------------------------------------------------------------------
FollowCappy:
//-------------------------------------------------------------------

	self waitthread FriendlyStop
	wait 0.1
	self.originaldestination	= level.cappy
	self.destination			= level.cappy
	
	wait 0.1
	self thread global/friendly.scr::friendlythink
	
	End
	

//-------------------------------------------------------------------
FinishLevel:
//
//	teleport near the boat, then run to the player and say my 2 bits.
//-------------------------------------------------------------------

	// "The bridge is heavily defended.  We can't cross there, but I found us another way across.  Follow me." maps "t "
	$lastgate_lockedtrigger delete
	self origin $FriendTeleportBoat2.origin
	self show

////	self waitthread global/say_to.scr::main dfr_T1L2_BP6704 level.cappy 200 0 0

	self exec global/enable_ai.scr

	self waitthread FriendlyRunTo level.cappy 200

	level.cappy turnto self
	level.cappy lookat self
	level.cappy waitthread FriendlyStop

	self waitthread gags/t1l2_cappy.scr::say_to dfr_T1L2_BP6704 12A003_Paradialog02 level.cappy 0
	
	// tell captain to go to the boat...
	level.cappy thread gags/t1l2_cappy.scr::GoToBoat
	$Objective3 text "Accompany Your Squad Across the River."
	$Objective3 TurnOn
	$Objective3 SetCurrent
	self waitthread FriendlyRunTo $TheBoat

	End

//-------------------------------------------------------------------
PlayerInBarn:
//-------------------------------------------------------------------

	dprintln "PLAYER IN BARN!"
	level.remind_player = 0

	End

//-------------------------------------------------------------------
RemindPlayer:
//	
//	Remind the player to go into the barn after the captain
//-------------------------------------------------------------------

	wait 1
	// trigger will set this to 0 if the player goes in the barn...
	self exec global/disable_ai.scr
	wait 4
	if ( level.remind_player==1 )
	{
		self exec global/disable_ai.scr
		self turnto $player
		self lookat $player
//		self anim 12A001_Paradialog01
//		self waittill animdone
		self say 12A001_Paradialog01
		wait 3

		self turnto NULL
		self lookat NULL
//		self waitthread gags/t1l2_cappy.scr::say_to dfr_T1L2_BP6703 12A001_Paradialog01 $player 0
	}
	self exec global/enable_ai.scr

	End

//-------------------------------------------------------------------
GoWaitForJump:
//
//	Go wait for cappy to tell the player his shiznit....
//-------------------------------------------------------------------

	self.go_wait_thread = local
	self waitthread FriendlyStop
	self waitthread FriendlyRunTo Flak88FriendlyWait
	self turnto level.cappy

	self.go_wait_thread = NULL

	End

//-------------------------------------------------------------------
GoWaitForJump2:
// stop what you're doing and get over there now!!!
//-------------------------------------------------------------------

	if ( self.go_wait_thread!=NULL )
		self.go_wait_thread delete

	self.go_wait_thread = local

	self exec global/disable_ai.scr
	self waitthread FriendlyRunTo Flak88FriendlyWait
	self exec global/enable_ai.scr

	local.bAttacked = 0


	// make sure we get to where we want to be... but still attack
	// enemies if needed...
	while ( 1 )
	{
		wait 0.4
		while (self.thinkstate == "attack")
		{
			waitframe
			local.bAttacked = 1
		}

		if ( local.bAttacked == 1 )
		{
			self waitthread FriendlyRunTo Flak88FriendlyWait
			local.bAttacked = 0
		}
	}

	self.go_wait_thread = NULL

	End

//-------------------------------------------------------------------
JumpOverWall:
//
//	Run to wall and play our animation to jump over...
//  Then, teleport to our next position...
//-------------------------------------------------------------------

	self turnto NULL

	// unlock the door before we go thru...

	$upstairs_door unlock
	$lastdoor_lockedtrigger delete

	if ( self.go_wait_thread!=NULL )
		self.go_wait_thread delete
	
	self.go_wait_thread = NULL

	self exec global/disable_ai.scr

	self waitthread FriendlyRunTo $jumpnow

	self turnto $WilsonLookHere
	self lookat $WilsonLookHere
	wait 1

	self turnto NULL
	self lookat NULL

	self anim_noclip 12A002_jumpwall
	self waittill animdone

	self runto $WilsonWalkThisWay
	self waittill movedone
	
	// now teleport to our next spot...
	self origin $FriendTeleportBoat2.origin
	self hide

	End

//-------------------------------------------------------------------
BlowUpGate:
//
//	Blow the gate under the bridge, then run in immediately...
//-------------------------------------------------------------------

	// let cappy say part of his dialog and then start running...
	wait 2

	self waitthread FriendlyStop

	self exec global/disable_ai.scr

	level.cappy nodamage
	level.friendly2 nodamage
	level.friendly3 nodamage

	self waitthread FriendlyRunTo $grate_demonode

	// MJG - added this turn b/c he was sometimes starting the run to the
	//		$grate_demonode from the right bank and would place the democharge
	//		on the left bank b/c that's what was straight ahead when he reached the node
	self turnto $demolookhere
	wait 0.25
	self turnto NULL

	self anim 12A004_setbomb
	self waittill animdone

//	local.explosive = spawn script_model model items/explosive.tik origin $demolookhere.origin
	local.explosive = spawn script_model model items/explosive.tik origin self.origin

	// MJG - added this turnto b/c he was occasionally getting stuck running
	//		backwards to the next node
	self turnto $grate_demonode
	self exec global/stand.scr
	wait 0.2
	self turnto NULL

	exec global/autosave.scr 2


	self waitthread FriendlyRunTo $demotrain

	self exec global/crouch.scr
	self turnto $demolookhere

	wait 3.2


//	wait 1

	self waitthread global/exploder.scr::explode 201
	local.ent = spawn script_model model fx/fx_waterexplosion.tik origin $demolookhere.origin
	local.ent anim start


	local.explosive delete

	// if player too close, KILL HIM!
	exec global/earthquake.scr .2 4 0 0	
	local.dist = vector_length($player.origin - $demolookhere.origin)
	if ( local.dist < 455 )
	{
		// make SURE player is far enough away...
		$player damage $world 15000 $world (0 0 0) (0 0 0) (0 0 0) 0 0 9 0
	}

	wait 0.25

	level.cappy takedamage
	level.friendly2 takedamage
	level.friendly3 takedamage

	self exec global/stand.scr

	// tell everyone to go...
	level.friendly1 thread StopGateCover

	if ( self==level.friendly3 )
	{
		level.friendly2 thread StopGateCover
	}

	self waitthread FriendlyRunTo $demowait

	// wait for him...  (we'll continue when player hits the underbridgetrigger)
	self turnto $player

	end

//-------------------------------------------------------------------
DoGateCover:
//-------------------------------------------------------------------
	self waitthread FriendlyStop
	self exec global/disable_ai.scr

	self waitthread FriendlyRunTo $friend3train

	self exec global/crouch.scr
	self turnto $demowait
	self lookat $demowait

	wait 0.75

	self turnto NULL
	self lookat NULL


	end

//-------------------------------------------------------------------
StopGateCover:
//-------------------------------------------------------------------

	self exec global/stand.scr

	wait 0.2

	self thread FriendlyStart


	end

//-------------------------------------------------------------------
BeFriendly:
//-------------------------------------------------------------------

//	self.destination			= $player
	self thread friendlythink
	self exec global/enable_ai.scr
	End

//-------------------------------------------------------------------
FriendlyStart:
//-------------------------------------------------------------------
	self thread global/friendly.scr::friendlythink

	wait 0.5
	self.health_pickup = FALSE

	End

//-------------------------------------------------------------------
FriendlyStop:
//-------------------------------------------------------------------
	self waitthread global/friendly.scr::friendlystop
//	self runto NULL

	End

//-------------------------------------------------------------------
FriendlyRunTo local.ent local.movedoneradius:
//
//	Makes sure we actually get there...
//-------------------------------------------------------------------

	if ( local.ent==NIL || local.ent==NULL )
	{
		dprintln "FriendlyRunTo: entity is NULL: " local.ent
		End
	}

	if ( self.runto_thread!=NIL && self.runto_thread!=NULL )
	{
		self.runto_thread delete
	}

	self.runto_thread	= local

	if ( local.movedoneradius==NIL )
	{
		self.movedoneradius	= 6
	}
	else
	{
		self.movedoneraidus = local.movedoneradius
	}

	self runto local.ent
	self waittill movedone

	while ( parm.movedone!=1 )
	{
		self runto local.ent
		self waittill movedone
	}
	self.runto_thread	= NULL

//	dprintln self.targetname " arrived at destination:" local.ent

	self.movedoneradius	= 250


	End


//-------------------------------------------------------------------
PrintDeathMsg:
//-------------------------------------------------------------------
	
	// see if we need to tell cappy to do his down2three quote...

	if ( level.friendly3dead==0 && level.friendly2dead==0 )
	{
		if ( level.mg42_gag==0 )
			level.cappy thread gags/t1l2_cappy.scr::DownToThree
	}

	if ( level.mg42_gag==1 )
	{
		if ( level.friendly3dead==0 && level.friendly2dead==0 )
		{
			// if neither is dead, then we're doing the mg42 gag right now.
			iprintlnbold "Private Galloway was killed in action."
			end
		}

		// if we've done the mg42 gag and this is the 2nd friendly going down, 
		// then it must be wilson..
		iprintlnbold "Private Wilson was killed in action."
	}
	else
	{
		if ( level.friendly3dead==0 && level.friendly2dead==0 )
		{
			// we haven't done mg42 yet and both were alive, it's wilson.
			iprintlnbold "Private Wilson was killed in action."
			end
		}

		// we haven't done mg42 yet, but this is the 2nd guy dead, it's galloway!
		iprintlnbold "Private Galloway was killed in action."
	}

	end

//-------------------------------------------------------------------
Friendly2DeathCheck:
//-------------------------------------------------------------------

	level.friendly2 waittill death

	waitthread PrintDeathMsg

	level.friendly2dead = 1

	thread LevelEndCheck

	level.longtermfriend = level.friendly3

	end

//-------------------------------------------------------------------
Friendly3DeathCheck:
//-------------------------------------------------------------------

	level.friendly3 waittill death

		// have him follow the cappy now that this guy is dead...
	level.friendly2.originaldestination		= level.cappy
	level.friendly2.destination				= level.cappy

	waitthread PrintDeathMsg

	level.friendly3dead = 1

	thread LevelEndCheck

	end

//-------------------------------------------------------------------
GetNervous:
// say a nervous remark as the captain opens the door to the last
// sequence...
//-------------------------------------------------------------------

	dprintln "private is GETTING NERVOUS!"

	wait 1

	// "I've got a bad feeling about this."
	self say dfr_T1L2_BP6735

	wait 5
	
	// Ambush!!
	self say dfr_T1L2_BP6736

	end

//-------------------------------------------------------------------
LevelEndCheck:
//-------------------------------------------------------------------

	if ( level.friendly2dead == 0 )
		end

	if ( level.friendly3dead == 0 )
		end

	iprintlnbold "Too many casualties."
	iprintlnbold "Mission failed."

	wait 3
	missionfailed

	end


// for debuging
FriendlyCheck:

	local.last = -1
	local.curr = -1

	while (isAlive self)
	{
		if ( self.friendly_stopped==1 )
			local.curr = 0
		else
			local.curr = 1

		if ( local.curr!=local.last )
		{
			if ( local.curr==1 )
				dprintln self.targetname " switched to friend."
			else
				dprintln self.targetname " switched to notfriend."
			
			local.last = local.curr
		}

		waitframe
	}

	end


//-------------------------------------------------------------------
RunForGrenadeCover local.spot:
// turn off ai and friendlyness and run to this spot.  Cappy will
// turn us back on when needed...
//-------------------------------------------------------------------

	self waitthread FriendlyStop
	self exec global/disable_ai.scr

	self waitthread FriendlyRunTo local.spot
	self turnto $Grenade1
	self lookat $Grenade1
	wait 0.75
	self turnto NULL
	self lookat NULL
	self exec global/enable_ai.scr

	
	end

