//-------------------------------------------------------------------
wrench local.spot local.lookspot:
//-------------------------------------------------------------------

	if(self.thinkstate != "idle")
	{
		wait 1
		end
	}
	
	self.interrupted = 0
	self.idle_thread = local
	self forceactivate
	self type_attack "alarm"
	self prealarmthread "wrenchinginterrupt"
	
	if(self.gun == "" || self.gun == NULL || self.gun == NIL || self.gun == "none")
		self weapon "walter p38"
	
	self holster
	waitframe
	
	if(self.isinplace != 1)
	{
		self.isinplace = 1
	   	self exec global/walkto.scr local.spot
 	  	self waittill movedone
     	self turnto local.lookspot
		self waittill turndone
		self turnto NULL
	}

	self curiousoff
	
	self waitthread weldinganimwait working_wrench_pullout
	self waitthread weldinganimwait working_wrenching_getdown

	self thread OnPain
	
	while( (isalive self) && (self.interrupted != 1) )
	{
		self waitthread weldinganimwait working_wrenching
	}

end

//-------------------------------------------------------------------
wrenchinginterrupt:
//-------------------------------------------------------------------

	if (self.idle_thread)
		self.idle_thread delete

	if( self.interrupted == 1 )
		end
	
	self.interrupted = 1

	self exec global/disable_ai.scr	
	self waitthread weldinganimwait working_wrenching_getup    
	self anim working_wrenching_putaway
	wait .5
	
	if(isalive self)
		self unholster
	
	wait .1
	
	if(isalive self)
	{
     	self exec global/enable_ai.scr
     	self attackplayer
	}	
	
	end

//-------------------------------------------------------------------
weldinganimwait local.anim:
//-------------------------------------------------------------------
	
	if(isalive self)
	{
		self anim local.anim
		self waittill animdone
	}
	else
	{
		waitframe
	}

	end

//-------------------------------------------------------------------
OnPain:
//-------------------------------------------------------------------

	self waittill pain

	self type_attack "turret"
	self thread wrenchinginterrupt

	end